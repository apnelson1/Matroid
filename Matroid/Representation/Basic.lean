import Mathlib.LinearAlgebra.LinearIndependent
import Matroid.Simple
-- import Matroid.ForMathlib.Function

variable {Î± Î² W W' ğ”½ R : Type*} {e f x : Î±} {I E B X Y : Set Î±} {M : Matroid Î±} [DivisionRing ğ”½]
  [AddCommGroup W] [Module ğ”½ W] [AddCommGroup W'] [Module ğ”½ W']

open Function Set Submodule FiniteDimensional BigOperators Matrix Set.Notation

section ForMathlib

@[simp] lemma linearIndependent_zero_iff : LinearIndependent ğ”½ (0 : Î± â†’ W) â†” IsEmpty Î± :=
  âŸ¨fun h â†¦ âŸ¨fun a â†¦ h.ne_zero a rflâŸ©, fun _ â†¦ linearIndependent_empty_typeâŸ©

@[simp] lemma restrict_zero (X : Set Î±) : X.restrict (0 : Î± â†’ W) = 0 := rfl

end ForMathlib

namespace Matroid

-- /-- A function `v : Î± â†’ W` represents `M` over `ğ”½` if independence of `I` in `M` corresponds to
-- linear independence of `v '' I` in `W`. -/
-- def IsRep (M : Matroid Î±) (ğ”½ : Type*) [CommSemiring ğ”½] [AddCommMonoid W] [Module ğ”½ W] (v : Î± â†’ W) :=
--   âˆ€ I, M.Indep I â†” LinearIndependent ğ”½ (I.restrict v)

structure Rep (M : Matroid Î±) (ğ”½ W : Type*) [Semiring ğ”½] [AddCommMonoid W] [Module ğ”½ W] where
  -- A representation assigns a vector to each element of `Î±`
  (to_fun : Î± â†’ W)
  -- A set is independent in `M` if and only if its image is linearly independent over `ğ”½` in `W`
  (valid' : âˆ€ I, M.Indep I â†” LinearIndependent ğ”½ (I.restrict to_fun))

instance : FunLike (M.Rep ğ”½ W) Î± W where
  coe v := v.to_fun
  coe_injective' := by rintro âŸ¨f,hâŸ© âŸ¨f', h'âŸ©; simp

-- instance : DFunLike (M.Rep ğ”½ W) Î± (fun _ â†¦ W) where
--   coe v := v.to_fun
--   coe_injective' := by rintro âŸ¨f,hâŸ© âŸ¨f', h'âŸ©; simp

-- instance coeFun : CoeFun (M.Rep ğ”½ W) fun _ â†¦ (Î± â†’ W) :=
--   âŸ¨DFunLike.coeâŸ©

@[simp] lemma Rep.to_fun_eq_coe (v : M.Rep ğ”½ W) : v.to_fun = (v : Î± â†’ W) := rfl

-- @[simp] lemma Rep.coe_mk (f : Î± â†’ W) (valid' : M.IsRep ğ”½ f) : (Rep.mk f valid' : Î± â†’ W) = f := rfl

-- lemma Rep.isRep (v : M.Rep ğ”½ W) : M.IsRep ğ”½ v := v.valid'

lemma Rep.indep_iff (v : M.Rep ğ”½ W) : M.Indep I â†” LinearIndependent ğ”½ (I.restrict v) :=
  v.valid' I

lemma Rep.indep_iff' (v : M.Rep ğ”½ W) : M.Indep I â†” LinearIndependent ğ”½ (fun x : I â†¦ v x) :=
  v.valid' I

lemma Rep.onIndep (v : M.Rep ğ”½ W) (hI : M.Indep I) : LinearIndependent ğ”½ (I.restrict v) :=
  v.indep_iff.1 hI

lemma Rep.injOn_of_indep (v : M.Rep ğ”½ W) (hI : M.Indep I) : InjOn v I :=
  injOn_iff_injective.2 ((v.onIndep hI).injective)

lemma Rep.indep_image (v : M.Rep ğ”½ W) (hI : M.Indep I) :
    LinearIndependent ğ”½ ((â†‘) : (v '' I) â†’ W) := by
  rw [â† linearIndependent_image <| v.injOn_of_indep hI]
  exact v.onIndep hI

lemma Rep.indep_iff_image_of_inj (v : M.Rep ğ”½ W) (h_inj : InjOn v I) :
    M.Indep I â†” LinearIndependent ğ”½ ((â†‘) : (v '' I) â†’ W) := by
  refine âŸ¨v.indep_image, fun hi â†¦ ?_âŸ©
  rw [v.indep_iff, restrict_eq]
  exact (linearIndependent_image h_inj (R := ğ”½)).2 hi

lemma Rep.indep_iff_image (v : M.Rep ğ”½ W) :
    M.Indep I â†” LinearIndependent ğ”½ ((â†‘) : (v '' I) â†’ W) âˆ§ InjOn v I :=
  âŸ¨fun h â†¦ âŸ¨v.indep_image h, v.injOn_of_indep hâŸ©,
    fun h â†¦ (v.indep_iff_image_of_inj h.2).2 h.1âŸ©

lemma Rep.eq_zero_iff_not_indep {v : M.Rep ğ”½ W} : v e = 0 â†” Â¬ M.Indep {e} := by
  simp [v.indep_iff, linearIndependent_unique_iff, -indep_singleton]

lemma Rep.eq_zero_of_not_mem_ground (v : M.Rep ğ”½ W) (he : e âˆ‰ M.E) : v e = 0 := by
  rw [v.eq_zero_iff_not_indep, indep_singleton]
  exact fun hl â†¦ he hl.mem_ground

lemma Rep.support_subset_ground (v : M.Rep ğ”½ W) : support v âŠ† M.E :=
  fun _ he â†¦ by_contra <| fun h' â†¦ he (v.eq_zero_of_not_mem_ground h')

lemma Rep.mem_ground_of_apply_ne_zero {v : M.Rep ğ”½ W} (hv : v e â‰  0) : e âˆˆ M.E :=
  v.support_subset_ground hv

lemma Indep.rep_apply_ne_zero_of_mem {v : M.Rep ğ”½ W} (hI : M.Indep I) (heI : e âˆˆ I) :
    v e â‰  0 := by
  rw [Ne, Rep.eq_zero_iff_not_indep, not_not]
  exact hI.subset (by simpa)

/-- A function with support contained in `M.E` that gives the correct independent sets
  within the ground set gives a representation -/
@[simps] def Rep.ofGround (f : Î± â†’ W) (h_support : support f âŠ† M.E)
    (hf : âˆ€ I âŠ† M.E, (M.Indep I â†” LinearIndependent ğ”½ (I.restrict f))) : M.Rep ğ”½ W where
  to_fun := f
  valid' := ( by
    intro I
    by_cases hI : I âŠ† M.E
    Â· rw [hf _ hI]
    rw [â† not_iff_not, iff_true_left (fun hi â†¦ hI hi.subset_ground)]
    intro h_ind
    obtain âŸ¨e, heI, heEâŸ© := not_subset.1 hI
    have h0 := h_ind.ne_zero âŸ¨e, heIâŸ©
    simp only [Function.comp_apply, ne_eq] at h0
    apply not_mem_subset h_support heE
    exact h0 )

@[simp] lemma Rep.ofGround_apply (f : Î± â†’ W) (hs : support f âŠ† M.E)
  (hf : âˆ€ I âŠ† M.E, (M.Indep I â†” LinearIndependent ğ”½ (I.restrict f))) (a : Î±) :
    Rep.ofGround f hs hf a = f a := rfl

/-- A function from `M.E` to a module determines a representation -/
@[simps!] noncomputable def Rep.ofSubtypeFun (f : M.E â†’ W) [DecidablePred (Â· âˆˆ M.E)]
    (hf : âˆ€ (I : Set M.E), M.Indep (Subtype.val '' I) â†” LinearIndependent ğ”½ (I.restrict f)) :
    M.Rep ğ”½ W :=
  Rep.ofGround
  ( fun a â†¦ if ha : a âˆˆ M.E then f âŸ¨a,haâŸ© else 0 )
  ( by aesop )
  ( by
    intro I hI
    rw [â† Subtype.range_val (s := M.E), subset_range_iff_exists_image_eq] at hI
    obtain âŸ¨I, rflâŸ© := hI
    rw [hf]
    apply linearIndependent_equiv' <| Equiv.Set.image _ _ Subtype.val_injective
    ext âŸ¨âŸ¨x,hxâŸ©, hx'âŸ©
    simp [dif_pos hx] )

-- @[simp] lemma Rep.offSubtypeFun_apply (f : M.E â†’ W) [DecidablePred (Â· âˆˆ M.E)]
--     (hf : âˆ€ {I : Set M.E}, M.Indep (Subtype.val '' I) â†” LinearIndependent ğ”½ (I.restrict f))
--     (e : M.E) : Rep.ofSubtypeFun f hf e = f e := by
--   simp [repOfSubtypeFun, rep_of_ground]

-- lemma repOfSubtypeFun_apply_mem (f : M.E â†’ W) [DecidablePred (Â· âˆˆ M.E)]
--     (hf : âˆ€ {I : Set M.E}, M.Indep (Subtype.val '' I) â†” LinearIndependent ğ”½ (I.restrict f))
--     {e : Î±} (he : e âˆˆ M.E) : repOfSubtypeFun f hf e = f âŸ¨e,heâŸ© := by
--   simp [repOfSubtypeFun, rep_of_ground, dif_pos he]

-- lemma repOfSubtypeFun_apply_not_mem (f : M.E â†’ W) [DecidablePred (Â· âˆˆ M.E)]
--     (hf : âˆ€ {I : Set M.E}, M.Indep (Subtype.val '' I) â†” LinearIndependent ğ”½ (I.restrict f))
--     {e : Î±} (he : e âˆ‰ M.E) : repOfSubtypeFun f hf e = 0 := by
--   simp [repOfSubtypeFun, rep_of_ground, dif_neg he]

-- lemma rep_of_ground_coe_eq (f : Î± â†’ W) (h_support : support f âŠ† M.E)
--   (hf : âˆ€ {I}, I âŠ† M.E â†’ (M.Indep I â†” LinearIndependent ğ”½ (f âˆ˜ ((â†‘) : I â†’ Î±)))) :
--   (rep_of_ground f h_support hf : Î± â†’ W) = f := rfl

/-- Compose a representation `v` with a linear map that is injective on the range of `v`-/
def Rep.map (v : M.Rep ğ”½ W) (Ïˆ : W â†’â‚—[ğ”½] W')
    (h_inj : Disjoint (span ğ”½ (range v)) (LinearMap.ker Ïˆ)) : M.Rep ğ”½ W' where
  to_fun := Ïˆ âˆ˜ v
  valid' := fun I â†¦ by
    rw [v.indep_iff, restrict_eq, restrict_eq, comp_assoc]
    refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
    Â· apply h.map (h_inj.mono_left (span_mono _))
      rw [Set.range_comp]
      exact image_subset_range _ _
    exact LinearIndependent.of_comp _ h

/-- Compose a representation with a linear injection. -/
def Rep.map' (v : M.Rep ğ”½ W) (Ïˆ : W â†’â‚—[ğ”½] W') (hÏˆ : LinearMap.ker Ïˆ = âŠ¥) := v.map Ïˆ (by simp [hÏˆ])

/-- Compose a representation with a linear equivalence. -/
def Rep.mapEquiv (v : M.Rep ğ”½ W) (Ïˆ : W â‰ƒâ‚—[ğ”½] W') : M.Rep ğ”½ W' := v.map' Ïˆ Ïˆ.ker

@[simp] lemma Rep.map'_apply (v : M.Rep ğ”½ W) (Ïˆ : W â†’â‚—[ğ”½] W') (hÏˆ : LinearMap.ker Ïˆ = âŠ¥) (e : Î±) :
    (v.map' Ïˆ hÏˆ) e = Ïˆ (v e) := rfl

@[simp] lemma Rep.mapEquiv_apply (v : M.Rep ğ”½ W) (Ïˆ : W â‰ƒâ‚—[ğ”½] W') (e : Î±) :
    (v.mapEquiv Ïˆ) e = Ïˆ (v e) := rfl

/--  Compose a representation with a module equality -/
def Rep.subtype_ofEq {Wâ‚ Wâ‚‚ : Submodule ğ”½ W} (v : M.Rep ğ”½ Wâ‚) (h : Wâ‚ = Wâ‚‚) : M.Rep ğ”½ Wâ‚‚ :=
  v.mapEquiv <| LinearEquiv.ofEq _ _ h

@[simp] lemma Rep.subtype_ofEq_apply {Wâ‚ Wâ‚‚ : Submodule ğ”½ W} (v : M.Rep ğ”½ Wâ‚) (h : Wâ‚ = Wâ‚‚)
    (e : Î±) : v.subtype_ofEq h e = LinearEquiv.ofEq _ _ h (v e) := rfl

/-- A representation gives a representation of any restriction -/
noncomputable def Rep.restrict (v : M.Rep ğ”½ W) (X : Set Î±) : (M â†¾ X).Rep ğ”½ W :=
  Rep.ofGround (indicator X v) ( by simp )
  ( by
    simp only [restrict_ground_eq, restrict_indep_iff]
    intro I hIX
    rw [v.indep_iff, and_iff_left hIX]
    convert Iff.rfl using 2
    ext âŸ¨e, heâŸ©
    simp [hIX he] )

@[simp] lemma Rep.restrict_apply (v : M.Rep ğ”½ W) (X : Set Î±) :
    (v.restrict X : Î± â†’ W) = indicator X v := rfl

/-- A representation gives a representation of a comap -/
def Rep.comap {M : Matroid Î²} (f : Î± â†’ Î²) (v : M.Rep ğ”½ W) : (M.comap f).Rep ğ”½ W :=
  Rep.ofGround (v âˆ˜ f)
  ( by
    simp only [comap_ground_eq, support_subset_iff, Function.comp_apply, ne_eq, mem_preimage]
    exact fun x â†¦ Not.imp_symm <| Rep.eq_zero_of_not_mem_ground _ )
  ( by
    intro I _
    rw [comap_indep_iff, v.indep_iff, restrict_eq, restrict_eq, comp_assoc]
    refine' âŸ¨fun âŸ¨h,hInjâŸ© â†¦ _, fun h â†¦ âŸ¨LinearIndependent.image_of_comp _ _ _ h, ?_âŸ©âŸ©
    Â· exact h.comp (imageFactorization f I) (hInj.imageFactorization_injective)
    rintro x hx y hy hxy
    have hi := h.injective (aâ‚ := âŸ¨x,hxâŸ©) (aâ‚‚ := âŸ¨y,hyâŸ©)
    simpa only [Function.comp_apply, Subtype.mk.injEq, hxy, true_imp_iff] using hi )

lemma Rep.comap_coeFun_eq {M : Matroid Î²} (f : Î± â†’ Î²) (v : M.Rep ğ”½ W) :
    (v.comap f : Î± â†’ W) = v âˆ˜ f := rfl

@[simp] lemma Rep.comap_apply {M : Matroid Î²} (f : Î± â†’ Î²) (v : M.Rep ğ”½ W) (a : Î±) :
    v.comap f a = v (f a) := rfl

def Rep.ofEq {M N : Matroid Î±} (v : M.Rep ğ”½ W) (h : M = N) : N.Rep ğ”½ W :=
  Rep.ofGround v
  ( v.support_subset_ground.trans_eq (congr_arg _ h) )
  ( by intro I _; rw [â† h, v.indep_iff] )

@[simp] lemma Rep.ofEq_apply {M N : Matroid Î±} (v : M.Rep ğ”½ W) (h : M = N) :
  (v.ofEq h : Î± â†’ W) = v := rfl

noncomputable def Rep.restrictSubtype (v : M.Rep ğ”½ W) (X : Set Î±) : (M.restrictSubtype X).Rep ğ”½ W :=
  (v.restrict X).comap ((â†‘) : X â†’ Î±)

lemma Rep.closure_eq (v : M.Rep ğ”½ W) (X : Set Î±) : M.closure X = (v â»Â¹' span ğ”½ (v '' X)) âˆ© M.E := by
  obtain âŸ¨I, hIâŸ© := M.exists_basis' X
  ext e
  by_cases heI : e âˆˆ I
  Â· refine iff_of_true ?_ (mem_inter ?_ ?_)
    Â· exact mem_closure_of_mem' _ (hI.subset heI) (hI.indep.subset_ground heI)
    exact subset_span (mem_image_of_mem v (hI.subset heI))
    exact hI.indep.subset_ground heI
  simp only [â† hI.closure_eq_closure, hI.indep.mem_closure_iff', v.indep_iff, restrict_def,
    linearIndependent_insert' heI, and_comm, heI, imp_false, not_and, mem_inter_iff, mem_preimage,
    SetLike.mem_coe, and_congr_right_iff]
  rw [â† v.indep_iff', iff_true_intro hI.indep, not_true, imp_false, not_not]
  refine fun he â†¦ âŸ¨fun h â†¦ mem_of_mem_of_subset h (span_mono (image_subset v hI.subset)),
    fun h â†¦ span_le.2 ?_ hâŸ©
  rintro _ âŸ¨f, hf, rflâŸ©
  refine (em (f âˆˆ I)).elim (fun hfI â†¦ subset_span <| mem_image_of_mem v hfI) (fun hfI â†¦ ?_)
  have hni := hI.insert_not_indep âŸ¨hf, hfIâŸ©
  rwa [v.indep_iff, restrict_def, linearIndependent_insert' hfI, â† v.indep_iff',
    and_iff_right hI.indep, not_not] at hni

lemma Rep.mem_closure_iff (v : M.Rep ğ”½ W) (heE : e âˆˆ M.E := by aesop_mat) :
    e âˆˆ M.closure X â†” v e âˆˆ span ğ”½ (v '' X) := by
  rw [v.closure_eq, mem_inter_iff, and_iff_left heE]
  rfl

/-- Transfer a `Rep` along a matroid map. The definition involves extending a function with zero,
so requires a `DecidablePred` assumption. -/
noncomputable def Rep.matroidMap (v : M.Rep ğ”½ W) (f : Î± â†’ Î²) (hf : M.E.InjOn f)
    [DecidablePred (âˆƒ y âˆˆ M.E, f y = Â·)] : (M.map f hf).Rep ğ”½ W :=
  let v' := fun (x : Î²) â†¦ if h : âˆƒ y âˆˆ M.E, f y = x then v h.choose else 0
  Rep.ofGround
  (f := v')
  ( h_support := fun x â†¦ by
      simp only [mem_support, map_ground, v']
      split_ifs with h
      Â· exact fun hne â†¦ âŸ¨_, v.support_subset_ground hne, h.choose_spec.2 âŸ©
      simp )
  ( hf := by
      have hv' : âˆ€ x âˆˆ M.E, v' (f x) = v x := by
        intro x hx
        have h : âˆƒ y âˆˆ M.E, f y = f x := âŸ¨x, hx, rflâŸ©
        simp only [v', dif_pos h, show h.choose = x from hf h.choose_spec.1 hx h.choose_spec.2]
      simp only [map_ground, map_indep_iff, forall_subset_image_iff]
      refine fun I hIE â†¦ âŸ¨fun âŸ¨I', hI', h_eqâŸ© â†¦ ?_, fun h â†¦ âŸ¨_, ?_, rflâŸ©âŸ©
      Â· obtain rfl : I = I' := (hf.image_eq_image_iff hIE hI'.subset_ground).1 h_eq
        refine LinearIndependent.image_of_comp (f := f) (s := I) _ ?_
        convert v.indep_iff.1 hI' using 1
        ext âŸ¨x, hxâŸ©
        simp [hv' _ (hIE hx)]
      rw [â† linearIndependent_equiv <| Equiv.Set.imageOfInjOn _ _ (hf.mono hIE)] at h
      rw [v.indep_iff]
      convert h
      ext âŸ¨x, hxâŸ©
      simp [Equiv.Set.imageOfInjOn, hv' _ (hIE hx)])

lemma Rep.matroidMap_apply (v : M.Rep ğ”½ W) {f : Î± â†’ Î²} {hf} [DecidablePred (âˆƒ y âˆˆ M.E, f y = Â·)]
    {x : Î±} (hx : x âˆˆ M.E) : v.matroidMap f hf (f x) = v x := by
  have h : âˆƒ y âˆˆ M.E, f y = f x := âŸ¨x, hx, rflâŸ©
  simp [matroidMap, dif_pos h, show h.choose = x from hf h.choose_spec.1 hx h.choose_spec.2]

lemma Rep.matroidMap_image (v : M.Rep ğ”½ W) (f : Î± â†’ Î²) (hf) [DecidablePred (âˆƒ y âˆˆ M.E, f y = Â·)]
    (hX : X âŠ† M.E) : v.matroidMap f hf '' (f '' X) = v '' X := by
  ext x
  simp only [mem_image, exists_exists_and_eq_and]
  constructor <;>
  Â· rintro âŸ¨a, ha, rflâŸ©
    exact âŸ¨a, ha, by rw [v.matroidMap_apply (hX ha)]âŸ©

/-- The `ğ”½`-representable matroid whose ground set is a vector space `W` over `ğ”½`,
and independence is linear independence.  -/
protected def onModule (ğ”½ W : Type*) [AddCommGroup W] [DivisionRing ğ”½] [Module ğ”½ W] : Matroid W :=
  IndepMatroid.matroid <| IndepMatroid.ofFinitaryCardAugment
  (E := univ)
  (Indep := fun I â†¦ LinearIndependent ğ”½ ((â†‘) : I â†’ W))
  (indep_empty := linearIndependent_empty _ _)
  (indep_subset := fun I J hJ hIJ â†¦ hJ.mono hIJ)
  (indep_aug := by
    intro I J hI hIfin hJ hJfin hIJ
    have hssu : Â¬ (J âŠ† span ğ”½ I) := by
      rw [â† span_le]
      refine fun hss â†¦ hIJ.not_le ?_
      have _ := hIfin.fintype
      have _ := hJfin.fintype
      have _ : Module.Finite ğ”½ (span ğ”½ I) := FiniteDimensional.span_of_finite _ hIfin
      rw [ncard_eq_toFinset_card' J, ncard_eq_toFinset_card' I, â† finrank_span_set_eq_card hJ,
        â† finrank_span_set_eq_card hI]
      exact Submodule.finrank_mono hss
    obtain âŸ¨a, haJ, haâŸ© := not_subset.1 hssu
    refine âŸ¨a, haJ, not_mem_subset subset_span ha, ?_âŸ©
    simp only [SetLike.mem_coe] at ha
    simpa [linearIndependent_insert (not_mem_subset subset_span ha), ha])
  (indep_compact := linearIndependent_of_finite)
  (subset_ground := by simp)

@[simps!] def repOnModule (ğ”½ W : Type*) [AddCommGroup W] [DivisionRing ğ”½] [Module ğ”½ W] :
    (Matroid.onModule ğ”½ W).Rep ğ”½ W where
  to_fun := id
  valid' _ := by rfl

-- lemma repOnModule_closure (X : Set W) : Matroid.onModule

/-- The `ğ”½`-representable matroid given by a function `f : Î± â†’ W` for a vector space `W` over `ğ”½`,
and a ground set `E : Set Î±`.  -/
protected def ofFun (ğ”½ : Type*) [DivisionRing ğ”½] [Module ğ”½ W] (E : Set Î±) (f : Î± â†’ W) : Matroid Î± :=
    (Matroid.onModule ğ”½ W).comapOn E f

noncomputable def repOfFun (ğ”½ : Type*) [DivisionRing ğ”½] [Module ğ”½ W] (E : Set Î±) (f : Î± â†’ W) :
    (Matroid.ofFun ğ”½ E f).Rep ğ”½ W :=
  ((repOnModule ğ”½ W).comap f).restrict E

@[simp] lemma repOfFun_coeFun_eq (ğ”½ : Type*) [DivisionRing ğ”½] [Module ğ”½ W] (E : Set Î±) (f : Î± â†’ W) :
    (repOfFun ğ”½ E f : Î± â†’ W) = indicator E f := rfl

instance matroidOfFun_finitary (ğ”½ : Type*) [DivisionRing ğ”½] [Module ğ”½ W] (f : Î± â†’ W) (E : Set Î±) :
    Finitary (Matroid.ofFun ğ”½ E f) := by
  rw [Matroid.ofFun, Matroid.onModule, comapOn]; infer_instance

lemma ofFun_finite (f : Î± â†’ W) (E : Set Î±) (hfin : E.Finite) : (Matroid.ofFun ğ”½ E f).Finite :=
  âŸ¨hfinâŸ©

@[simp] lemma ofFun_ground_eq {f : Î± â†’ W} {E : Set Î±} : (Matroid.ofFun ğ”½ E f).E = E := rfl

@[simp] lemma ofFun_indep_iff {f : Î± â†’ W} {E : Set Î±} :
    (Matroid.ofFun ğ”½ E f).Indep I â†” LinearIndependent ğ”½ (I.restrict f) âˆ§ I âŠ† E := by
  rw [Matroid.ofFun, comapOn_indep_iff]
  by_cases hinj : InjOn f I
  Â· simp only [Matroid.onModule, IndepMatroid.matroid_Indep, and_iff_right hinj,
    IndepMatroid.ofFinitaryCardAugment_indep, â† linearIndependent_image hinj, and_congr_left_iff]
    exact fun _ â†¦ Iff.rfl
  exact iff_of_false (by simp [hinj]) fun hli â†¦ hinj <| injOn_iff_injective.2 hli.1.injective

lemma ofFun_congr {v v' : Î± â†’ W} (hvv' : EqOn v v' E) :
    Matroid.ofFun ğ”½ E v = Matroid.ofFun ğ”½ E v' := by
  refine ext_indep rfl fun I (hI : I âŠ† E) â†¦ ?_
  simp only [ofFun_indep_iff, hI, and_true]
  convert Iff.rfl using 2
  ext âŸ¨e, heâŸ©
  rw [restrict_apply, restrict_apply, hvv']
  exact hI he

@[simp] lemma ofFun_indicator {v : Î± â†’ W} :
    Matroid.ofFun ğ”½ E (E.indicator v) = Matroid.ofFun ğ”½ E v :=
  ofFun_congr <| eqOn_indicator

lemma ofFun_closure_eq {v : Î± â†’ W} {E : Set Î±} (hvE : support v âŠ† E) :
    (Matroid.ofFun ğ”½ E v).closure X = v â»Â¹' (span ğ”½ (v '' X)) âˆ© E := by
  rw [(repOfFun ğ”½ E v).closure_eq, repOfFun_coeFun_eq, ofFun_ground_eq, indicator_preimage]
  simp +contextual [indicator_eq_self.2 hvE]

lemma ofFun_closure_eq_of_subset_ground {v : Î± â†’ W} {E : Set Î±} (hXE : X âŠ† E) :
    (Matroid.ofFun ğ”½ E v).closure X = v â»Â¹' (span ğ”½ (v '' X)) âˆ© E := by
  rw [â† ofFun_indicator, ofFun_closure_eq (by simp), indicator_preimage,
    ((Set.eqOn_indicator (f := v)).mono hXE).image_eq]
  simp

lemma _root_.Basis.ofFun_base {v : Î± â†’ W} {E : Set Î±} {B : Set Î±} (b : _root_.Basis B ğ”½ W)
    (hfb : âˆ€ x : B, v x = b x) (hBE : B âŠ† E) : (Matroid.ofFun ğ”½ E v).Base B := by
  have hrw : v '' B = range b := by simp_rw [Set.ext_iff, mem_range, â† hfb]; aesop

  refine Indep.base_of_ground_subset_closure ?_ ?_
  Â· rw [Matroid.ofFun_indep_iff, restrict_eq, and_iff_left hBE]
    convert b.linearIndependent
    ext e
    exact hfb e
  rw [ofFun_closure_eq_of_subset_ground hBE, hrw, b.span_eq]
  simp





-- @[simp] lemma ofFun_zero (ğ”½ : Type*) [Field ğ”½] [Module ğ”½ W] (E : Set Î±) :

-- @[simp] lemma linearIndependent_zero_iff @[simp] lemma ofFun_zero (ğ”½ : Type*) [Field ğ”½] [Module ğ”½ W] (E : Set Î±) :

@[simp] lemma ofFun_zero (ğ”½ : Type*) [Field ğ”½] [Module ğ”½ W] (E : Set Î±) :
    (Matroid.ofFun ğ”½ E (0 : Î± â†’ W)) = loopyOn E := by
  simp +contextual [eq_loopyOn_iff]
