import Matroid.Flat
import Mathlib.LinearAlgebra.FiniteDimensional

variable {Î± Î² W W' ğ”½ : Type _} {e f x : Î±} {I B X : Set Î±} {M : Matroid Î±} [Field ğ”½] 
  [AddCommGroup W] [Module ğ”½ W] [AddCommGroup W'] [Module ğ”½ W']

open Function Set Submodule


theorem linearIndependent_subtype_congr {R M : Type _} [Semiring R] [AddCommMonoid M] [Module R M] 
  {s s' : Set M} (h_eq : s = s') : 
    LinearIndependent R ((â†‘) : s â†’ M) â†” LinearIndependent R ((â†‘) : s' â†’ M) := by 
  subst h_eq; rfl 

namespace Matroid

/-- A function `Ï† : Î± â†’ W` represents `M` over `ğ”½` if independence in `M` corresponds to linear
  independence in `W` of the image. -/
def IsRep (M : Matroid Î±) (ğ”½ : Type _) [Field ğ”½] [AddCommMonoid W] [Module ğ”½ W] (Ï† : Î± â†’ W) := 
  âˆ€ I, M.Indep I â†” LinearIndependent ğ”½ (I.restrict Ï†)

@[pp_dot] structure Rep (M : Matroid Î±) (ğ”½ W : Type _) [Field ğ”½] [AddCommMonoid W] 
  [Module ğ”½ W] where
  -- A representation assigns a vector to each element of `Î±`
  (to_fun : Î± â†’ W)
  -- A set is independent in `M` if and only if its image is linearly independent over `ğ”½` in `W`
  (valid' : M.IsRep ğ”½ to_fun)
 
instance : FunLike (M.Rep ğ”½ W) Î± (fun _ â†¦ W) where
  coe Ï† := Ï†.to_fun
  coe_injective' := by rintro âŸ¨f,hâŸ© âŸ¨f', h'âŸ©; simp 
  
instance coeFun : CoeFun (M.Rep ğ”½ W) fun _ â†¦ (Î± â†’ W) :=
  âŸ¨FunLike.coeâŸ©

@[simp] theorem Rep.to_fun_eq_coe (Ï† : M.Rep ğ”½ W) : Ï†.to_fun = (Ï† : Î± â†’ W) := rfl

theorem Rep.indep_iff (Ï† : M.Rep ğ”½ W) : M.Indep I â†” LinearIndependent ğ”½ (I.restrict Ï†) := 
  Ï†.valid' I

theorem Rep.linear_indep (Ï† : M.Rep ğ”½ W) (hI : M.Indep I) : LinearIndependent ğ”½ (I.restrict Ï†) := 
  Ï†.indep_iff.1 hI 

theorem Rep.injOn_of_indep (Ï† : M.Rep ğ”½ W) (hI : M.Indep I) : InjOn Ï† I :=
  injOn_iff_injective.2 ((Ï†.linear_indep hI).injective)
  
theorem Rep.linear_indep_image (Ï† : M.Rep ğ”½ W) (hI : M.Indep I) : 
    LinearIndependent ğ”½ ((â†‘) : Ï† '' I â†’ W) := by
  rw [â†linearIndependent_image (Ï†.injOn_of_indep hI)]
  exact Ï†.linear_indep hI 

theorem Rep.indep_iff_image (Ï† : M.Rep ğ”½ W) (h_inj : InjOn Ï† I) :
    M.Indep I â†” LinearIndependent ğ”½ ((â†‘) : Ï† '' I â†’ W) := by 
  refine âŸ¨Ï†.linear_indep_image, fun hi â†¦ ?_âŸ© 
  rw [Ï†.indep_iff, restrict_eq]
  exact (linearIndependent_image h_inj (R := ğ”½)).2 hi

theorem Rep.eq_zero_iff_not_indep {Ï† : M.Rep ğ”½ W} : Ï† e = 0 â†” Â¬ M.Indep {e} := by
  simp [Ï†.indep_iff, linearIndependent_unique_iff, -indep_singleton]
  
theorem Rep.eq_zero_of_not_mem_ground (Ï† : M.Rep ğ”½ W) (he : e âˆ‰ M.E) : Ï† e = 0 := by 
  rw [Ï†.eq_zero_iff_not_indep, indep_singleton]
  exact fun hl â†¦ he hl.mem_ground

theorem Rep.support_subset_ground (Ï† : M.Rep ğ”½ W) : support Ï† âŠ† M.E := 
  fun _ he â†¦ by_contra <| fun h' â†¦ he (Ï†.eq_zero_of_not_mem_ground h')


/-- A function with support contained in `M.E` that gives the correct independent sets 
  within the ground set gives a representation -/
def rep_of_ground (f : Î± â†’ W) (h_support : support f âŠ† M.E) 
    (hf : âˆ€ {I}, I âŠ† M.E â†’ (M.Indep I â†” LinearIndependent ğ”½ (I.restrict f))) : M.Rep ğ”½ W where
  to_fun := f
  valid' := ( by 
    intro I
    obtain (hI | hI) := em (I âŠ† M.E)
    Â· rw [hf hI]
    rw [â†not_iff_not, iff_true_left (fun hi â†¦ hI hi.subset_ground)]
    intro h_ind
    obtain âŸ¨e, heI, heEâŸ© := not_subset.1 hI
    have h0 := h_ind.ne_zero âŸ¨e, heIâŸ© 
    simp only [comp_apply, ne_eq] at h0  
    apply not_mem_subset h_support heE
    exact h0 )

@[simp] theorem rep_of_ground_coe_eq (f : Î± â†’ W) (h_support : support f âŠ† M.E) 
  (hf : âˆ€ {I}, I âŠ† M.E â†’ (M.Indep I â†” LinearIndependent ğ”½ (f âˆ˜ ((â†‘) : I â†’ Î±)))) : 
  (rep_of_ground f h_support hf : Î± â†’ W) = f := rfl 

  
def Rep.map (Ï† : M.Rep ğ”½ W) (Ïˆ : W â†’â‚—[ğ”½] W') 
    (h_inj : Disjoint (span ğ”½ (range Ï†)) (LinearMap.ker Ïˆ)) : M.Rep ğ”½ W' where 
  to_fun := Ïˆ âˆ˜ Ï†
  valid' := fun I â†¦ by 
    rw [Ï†.indep_iff, restrict_eq, restrict_eq, comp.assoc] 
    refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
    Â· apply h.map (h_inj.mono_left (span_mono _))
      rw [range_comp]
      exact image_subset_range _ _
    exact LinearIndependent.of_comp _ h

/-- Compose a representation with a linear injection. -/
def Rep.map' (Ï† : M.Rep ğ”½ W) (Ïˆ : W â†’â‚—[ğ”½] W') (hÏˆ : LinearMap.ker Ïˆ = âŠ¥) := Ï†.map Ïˆ (by simp [hÏˆ])

/-- Compose a representation with a linear equivalence. -/
def Rep.map_equiv (Ï† : M.Rep ğ”½ W) (Ïˆ : W â‰ƒâ‚—[ğ”½] W') : M.Rep ğ”½ W' := Ï†.map' Ïˆ Ïˆ.ker

@[simp] theorem Rep.map'_apply (Ï† : M.Rep ğ”½ W) (Ïˆ : W â†’â‚—[ğ”½] W') (hÏˆ : LinearMap.ker Ïˆ = âŠ¥) (e : Î±) : 
    (Ï†.map' Ïˆ hÏˆ) e = Ïˆ (Ï† e) := rfl 

@[simp] theorem Rep.map_equiv_apply (Ï† : M.Rep ğ”½ W) (Ïˆ : W â‰ƒâ‚—[ğ”½] W') (e : Î±) : 
    (Ï†.map_equiv Ïˆ) e = Ïˆ (Ï† e) := rfl 

def Rep.subtype_ofEq {Wâ‚ Wâ‚‚ : Submodule ğ”½ W} (Ï† : M.Rep ğ”½ Wâ‚) (h : Wâ‚ = Wâ‚‚) : M.Rep ğ”½ Wâ‚‚ := 
  Ï†.map_equiv <| LinearEquiv.ofEq _ _ h

-- @[simp] theorem Rep.subtype_ofEq_apply {Wâ‚ Wâ‚‚ : Submodule ğ”½ W} (Ï† : M.Rep ğ”½ Wâ‚) (h : Wâ‚ = Wâ‚‚) 
--   (e : Î±) : (Ï†.subtype_ofEq h) e = âŸ¨Ï† e, h â–¸ (Ï† e).propâŸ© := rfl 

/-- Each function from a type to a module defines a matroid on a finite superset of its support -/
def matroid_of_fun (f : Î± â†’ W) (E : Set Î±) (hf : f.support âŠ† E) (hfin : E.Finite) : Matroid Î± := 
  matroid_of_indep_of_finite 
  hfin 
  ( fun I â†¦ LinearIndependent ğ”½ (I.restrict f) ) 
  ( linearIndependent_empty_type )
  ( fun I J hI hJI â†¦ by convert hI.comp _ (inclusion_injective hJI) )
 
  ( by 
    intro I J hI hJ hIJ
    have hIinj : InjOn f I := by rw [injOn_iff_injective]; exact hI.injective

    have : Fintype I
    Â· refine Finite.fintype (hfin.subset (subset_trans (fun _ hxI â†¦ ?_) hf))
      exact hI.ne_zero âŸ¨_, hxIâŸ©
      
    have : Fintype J 
    Â· refine Finite.fintype (hfin.subset (subset_trans (fun _ hxJ â†¦ ?_) hf))
      exact hJ.ne_zero âŸ¨_, hxJâŸ©

    have h : Â¬ (f '' J âŠ† span ğ”½ (f '' I))
    Â· refine fun hss â†¦ hIJ.not_le ?_
      rw [â†range_restrict, â†range_restrict] at hss
      
      have : FiniteDimensional ğ”½ {x // x âˆˆ span ğ”½ (range (restrict I f))}
      Â· apply FiniteDimensional.span_of_finite
        rw [range_restrict]
        apply I.toFinite.image
      
      have hle := span_mono hss (R := ğ”½)
      simp only [span_coe_eq_restrictScalars, restrictScalars_self] at hle  
      have hrank := finrank_le_finrank_of_le hle 
      rwa [finrank_span_eq_card hI, finrank_span_eq_card hJ, 
        â†Nat.card_eq_fintype_card, â†Nat.card_eq_fintype_card, 
        Nat.card_coe_set_eq, Nat.card_coe_set_eq] at hrank
  
    obtain âŸ¨_, âŸ¨e, he, rflâŸ©, heIâŸ© := not_subset.1 h
    have' heI' : f e âˆ‰ f '' I := fun h â†¦ heI (Submodule.subset_span h)
    have heI'' : e âˆ‰ I := fun h' â†¦ heI' (mem_image_of_mem f h') 
    refine' âŸ¨e, he, heI'', _âŸ©
    simp only
    have hi : LinearIndependent ğ”½ ((â†‘) : f '' I â†’ W)
    Â· rwa [â† linearIndependent_image hIinj]

    have hins := (linearIndependent_insert heI').2 âŸ¨hi, heIâŸ©
    
    apply hins.comp (Equiv.setCongr image_insert_eq âˆ˜ (imageFactorization f (insert e I)))
    simp only [EmbeddingLike.comp_injective]
    apply imageFactorization_injective
    rwa [injOn_insert heI'', and_iff_left (fun h â†¦ heI (Submodule.subset_span h))] ) 
  ( by 
    refine fun I hI â†¦ subset_trans (fun e heI â†¦ ?_) hf
    exact hI.ne_zero âŸ¨_, heIâŸ© )

@[simp] theorem matroid_of_fun_indep_iff (f : Î± â†’ W) (E : Set Î±) (hf : f.support âŠ† E) 
  (hfin : E.Finite) (I : Set Î±) : 
    (matroid_of_fun (ğ”½ := ğ”½) f E hf hfin).Indep I â†” LinearIndependent ğ”½ (I.restrict f) := by
  simp [matroid_of_fun, matroid_of_indep_of_finite] 

def rep_of_fun (f : Î± â†’ W) (E : Set Î±) (hf : f.support âŠ† E) (hfin : E.Finite) :
    (matroid_of_fun (ğ”½ := ğ”½) f E hf hfin).Rep ğ”½ W where 
  to_fun := f
  valid' := by simp [IsRep]

theorem Rep.range_subset_span_base (Ï† : M.Rep ğ”½ W) (hB : M.Base B) : range Ï† âŠ† span ğ”½ (Ï† '' B) := by 
  rintro _ âŸ¨e, he ,rflâŸ© 
  
  obtain (heB | heB) := em (e âˆˆ B)
  Â· exact subset_span (mem_image_of_mem _ heB)
  by_contra h'
  have hind := LinearIndependent.insert ?_ h'
  
  Â· rw [â†linearIndependent_subtype_congr image_insert_eq, â†Ï†.indep_iff_image] at hind
    Â· exact heB (hB.mem_of_insert_indep hind) 
    rw [injOn_insert heB, and_iff_right (Ï†.injOn_of_indep hB.indep)]
    exact fun h'' â†¦ h' <| mem_of_mem_of_subset h'' subset_span
  exact Ï†.linear_indep_image hB.indep
  
theorem Rep.span_range_eq_span_base (Ï† : M.Rep ğ”½ W) (hB : M.Base B) : 
     span ğ”½ (range (restrict B Ï†)) = span ğ”½ (range Ï†) := by 
  rw [range_restrict, eq_comm]
  exact span_eq_of_le _ (Ï†.range_subset_span_base hB) (span_mono (image_subset_range _ _))

/-- A representation is `FullRank` if its vectors span the space -/
def Rep.FullRank (Ï† : M.Rep ğ”½ W) : Prop := âŠ¤ â‰¤ span ğ”½ (range Ï†)

/-- Restrict a representation to the submodule spanned by its image -/
def Rep.restrict_span (Ï† : M.Rep ğ”½ W) : M.Rep ğ”½ (span ğ”½ (range Ï†)) where
  to_fun := inclusion subset_span âˆ˜ rangeFactorization Ï†
  valid' := (by 
    intro I
    rw [Ï†.indep_iff]
    refine âŸ¨fun h â†¦ LinearIndependent.of_comp (Submodule.subtype _) (by rwa [coeSubtype]), 
      fun h â†¦ h.map' (Submodule.subtype _) (ker_subtype _)âŸ© )


theorem Rep.FullRank.span_range {Ï† : M.Rep ğ”½ W} (h : Ï†.FullRank) : span ğ”½ (range Ï†) = âŠ¤ := by 
  rwa [eq_top_iff]

@[simp] theorem Rep.restrict_span_apply (Ï† : M.Rep ğ”½ W) (e : Î±) : 
  Ï†.restrict_span e = inclusion subset_span (rangeFactorization Ï† e) := rfl 

theorem Rep.restrict_span_fullRank (Ï† : M.Rep ğ”½ W) : 
    Ï†.restrict_span.FullRank := by
  change _ â‰¤ span ğ”½ (range (inclusion subset_span âˆ˜ _))
  rw [range_comp, surjective_onto_range.range_eq, image_univ, range_inclusion]
  change _ â‰¤ span ğ”½ ((Submodule.subtype (span ğ”½ (range â†‘Ï†))) â»Â¹' _) 
  simp

noncomputable def Rep.FullRank.basis_of_base {Ï† : M.Rep ğ”½ W} (h : Ï†.FullRank) (hB : M.Base B) : 
    _root_.Basis B ğ”½ W := 
  Basis.mk (Ï†.linear_indep hB.indep) 
    ( by rw [â†h.span_range, Ï†.span_range_eq_span_base hB] )

noncomputable def Rep.basis_of_base (Ï† : M.Rep ğ”½ W) (hB : M.Base B) : 
    _root_.Basis B ğ”½ (span ğ”½ (range Ï†)) := 
  (Basis.span (Ï†.linear_indep hB.indep)).map <| LinearEquiv.ofEq _ _ (Ï†.span_range_eq_span_base hB)

/-- The natural representation with rows indexed by a base -/
noncomputable def Rep.to_standard_rep (Ï† : M.Rep ğ”½ W) (hB : M.Base B) :
    M.Rep ğ”½ (B â†’â‚€ ğ”½) :=
  Ï†.restrict_span.map_equiv (Ï†.restrict_span_fullRank.basis_of_base hB).repr 
  
theorem Rep.standard_rep_eq_one (Ï† : M.Rep ğ”½ W) (hB : M.Base B) (e : B) : 
    (Ï†.to_standard_rep hB) e e = 1 := by 
  simp only [Rep.to_standard_rep, Rep.FullRank.basis_of_base, Rep.map_equiv_apply, 
    Rep.restrict_span_apply, Basis.mk_repr]
  rw [LinearIndependent.repr_eq_single (i := e) _ _ (by simp)]
  simp
  
theorem Rep.standard_rep_eq_zero (Ï† : M.Rep ğ”½ W) (hB : M.Base B) (e f : B) (hef : e â‰  f) : 
    (Ï†.to_standard_rep hB) e f = 0 := by 
  simp [Rep.to_standard_rep, Rep.FullRank.basis_of_base, Rep.map_equiv_apply, 
    Rep.restrict_span_apply, Basis.mk_repr]
  rw [LinearIndependent.repr_eq_single (i := e) _ _ (by simp)]
  exact Finsupp.single_eq_of_ne hef
  