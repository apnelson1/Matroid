-- import Mathlib.LinearAlgebra.LinearIndependent
import Mathlib.LinearAlgebra.Projectivization.Basic
import Matroid.Connectivity.Skew
-- import Matroid.ForMathlib.LinearAlgebra.LinearIndependent
import Matroid.ForMathlib.LinearAlgebra.LinearIndepOn

variable {Î± Î² W W' ğ”½ R : Type*} {e f x : Î±} {I E B X Y : Set Î±} {M : Matroid Î±} [DivisionRing ğ”½]
  [AddCommGroup W] [Module ğ”½ W] [AddCommMonoid W'] [Module ğ”½ W']

open Function Set Submodule FiniteDimensional BigOperators Matrix Set.Notation
universe u v

namespace Matroid

/-- `M.Rep ğ”½ W` is a function from `Î±` to a module `W` that represents `M`. -/
@[ext] structure Rep (M : Matroid Î±) (ğ”½ W : Type*) [Semiring ğ”½] [AddCommMonoid W] [Module ğ”½ W] where
  -- A representation assigns a vector to each element of `Î±`
  (to_fun : Î± â†’ W)
  -- A set is independent in `M` if and only if its image is linearly independent over `ğ”½` in `W`
  (indep_iff' : âˆ€ I, M.Indep I â†” LinearIndepOn ğ”½ to_fun I)


instance : FunLike (M.Rep ğ”½ W) Î± W where
  coe v := v.to_fun
  coe_injective' := by rintro âŸ¨f,hâŸ© âŸ¨f', h'âŸ©; simp

@[simp] lemma Rep.to_fun_eq_coe (v : M.Rep ğ”½ W) : v.to_fun = (v : Î± â†’ W) := rfl

lemma Rep.indep_iff (v : M.Rep ğ”½ W) : M.Indep I â†” LinearIndepOn ğ”½ v I :=
  v.indep_iff' I

lemma Rep.onIndep (v : M.Rep ğ”½ W) (hI : M.Indep I) : LinearIndepOn ğ”½ v I :=
  v.indep_iff.1 hI

lemma Rep.injOn_of_indep (v : M.Rep ğ”½ W) (hI : M.Indep I) : InjOn v I :=
  injOn_iff_injective.2 ((v.onIndep hI).injective)

-- lemma Rep.indep_image (v : M.Rep ğ”½ W) (hI : M.Indep I) :
--     LinearIndependent ğ”½ ((â†‘) : (v '' I) â†’ W) := by
--   rw [â† linearIndependent_image <| v.injOn_of_indep hI]
--   exact v.onIndep hI

-- lemma Rep.indep_iff_image_of_inj (v : M.Rep ğ”½ W) (h_inj : InjOn v I) :
--     M.Indep I â†” LinearIndependent ğ”½ ((â†‘) : (v '' I) â†’ W) := by
--   refine âŸ¨v.indep_image, fun hi â†¦ ?_âŸ©
--   rw [v.indep_iff]
--   exact (linearIndependent_image h_inj (R := ğ”½)).2 hi

-- lemma Rep.indep_iff_image (v : M.Rep ğ”½ W) :
--     M.Indep I â†” LinearIndependent ğ”½ ((â†‘) : (v '' I) â†’ W) âˆ§ InjOn v I :=
--   âŸ¨fun h â†¦ âŸ¨v.indep_image h, v.injOn_of_indep hâŸ©,
--     fun h â†¦ (v.indep_iff_image_of_inj h.2).2 h.1âŸ©

lemma Rep.eq_zero_iff_not_indep {v : M.Rep ğ”½ W} : v e = 0 â†” Â¬ M.Indep {e} := by
  simp [v.indep_iff]

lemma Rep.eq_zero_iff (v : M.Rep ğ”½ W) (e : Î±) (he : e âˆˆ M.E := by aesop_mat) :
    v e = 0 â†” M.IsLoop e := by
  rw [v.eq_zero_iff_not_indep, singleton_not_indep]

lemma Rep.eq_zero_of_not_mem_ground (v : M.Rep ğ”½ W) (he : e âˆ‰ M.E) : v e = 0 := by
  rw [v.eq_zero_iff_not_indep, indep_singleton]
  exact fun hl â†¦ he hl.mem_ground

lemma Rep.eq_zero_of_isLoop (v : M.Rep ğ”½ W) (h : M.IsLoop e) : v e = 0 :=
  (v.eq_zero_iff e).2 h

lemma Rep.ne_zero_of_isNonloop (v : M.Rep ğ”½ W) (h : M.IsNonloop e) : v e â‰  0 := by
  rw [Ne, v.eq_zero_iff e]; exact h.not_isLoop

lemma Rep.ne_zero_iff_isNonloop (v : M.Rep ğ”½ W) (e : Î±) : v e â‰  0 â†” M.IsNonloop e := by
  refine âŸ¨fun hne â†¦ ?_, v.ne_zero_of_isNonloopâŸ©
  by_cases he : e âˆˆ M.E
  Â· rwa [â† not_isLoop_iff, â† v.eq_zero_iff e]
  simp [v.eq_zero_of_not_mem_ground he] at hne

lemma Rep.support_subset_ground (v : M.Rep ğ”½ W) : support v âŠ† M.E :=
  fun _ he â†¦ by_contra <| fun h' â†¦ he (v.eq_zero_of_not_mem_ground h')

lemma Rep.mem_ground_of_apply_ne_zero {v : M.Rep ğ”½ W} (hv : v e â‰  0) : e âˆˆ M.E :=
  v.support_subset_ground hv

lemma Indep.rep_apply_ne_zero_of_mem {v : M.Rep ğ”½ W} (hI : M.Indep I) (heI : e âˆˆ I) :
    v e â‰  0 := by
  rw [Ne, Rep.eq_zero_iff_not_indep, not_not]
  exact hI.subset (by simpa)

lemma Rep.isBasis'_iff (v : M.Rep ğ”½ W) :
    M.IsBasis' I X â†” I âŠ† X âˆ§ LinearIndepOn ğ”½ v I âˆ§ v '' X âŠ† span ğ”½ (v '' I) := by
  have aux â¦ƒI J : Set Î±â¦„ : M.Indep J âˆ§ J âŠ† X â†’ I âŠ† J â†’ M.Indep I âˆ§ I âŠ† X :=
    fun h hJI â†¦ âŸ¨h.1.subset hJI, hJI.trans h.2âŸ©
  simp only [IsBasis', maximal_iff_forall_insert aux, insert_subset_iff, not_and, image_subset_iff]
  simp +contextual only [v.indep_iff, linearIndepOn_insert_iff, imp_false, and_imp, iff_def,
    true_and, not_true_eq_false, not_imp_not, forall_const, and_self]
  refine âŸ¨fun hI hIX h e heX â†¦ (em (e âˆˆ I)).elim (fun heI â†¦ ?_) fun heI â†¦ h e heI heX,
    fun hIX hI hX e heI heX â†¦ hX heXâŸ©
  exact mem_of_mem_of_subset heI <| (subset_preimage_image v I).trans <| preimage_mono subset_span

lemma Rep.mem_closure_iff (v : M.Rep ğ”½ W) (heE : e âˆˆ M.E := by aesop_mat) :
    e âˆˆ M.closure X â†” v e âˆˆ span ğ”½ (v '' X) := by
  obtain âŸ¨I, hIXâŸ© := M.exists_isBasis' X
  have aux : span ğ”½ (v '' I) = span ğ”½ (v '' X) :=
    (span_mono (image_mono hIX.subset)).antisymm <| span_le.2 (v.isBasis'_iff.1 hIX).2.2
  rw [â† hIX.closure_eq_closure, â† aux, â† not_iff_not, (v.onIndep hIX.indep).not_mem_span_iff,
    hIX.indep.not_mem_closure_iff, v.indep_iff]

lemma Rep.closure_eq (v : M.Rep ğ”½ W) (X : Set Î±) : M.closure X = (v â»Â¹' span ğ”½ (v '' X)) âˆ© M.E := by
  ext e
  by_cases he : e âˆˆ M.E
  Â· rw [v.mem_closure_iff, mem_inter_iff, and_iff_left he, mem_preimage, SetLike.mem_coe]
  simp [he, not_mem_subset (M.closure_subset_ground X) he]

lemma Rep.mem_closure_iff' (v : M.Rep ğ”½ W) :
    e âˆˆ M.closure X â†” v e âˆˆ span ğ”½ (v '' X) âˆ§ e âˆˆ M.E := by
  simp [v.closure_eq]

lemma Rep.span_le_of_closure_subset (v : M.Rep ğ”½ W) (hXY : M.closure X âŠ† M.closure Y) :
    span ğ”½ (v '' X) â‰¤ span ğ”½ (v '' Y) := by
  rw [span_le]
  rintro _ âŸ¨e, he, rflâŸ©
  obtain heE | heE := em' (e âˆˆ M.E)
  Â· simp [v.eq_zero_of_not_mem_ground heE]
  rw [v.closure_eq Y] at hXY
  exact (hXY (M.mem_closure_of_mem' he heE)).1

lemma Rep.closure_subset_iff_span_le (v : M.Rep ğ”½ W) :
    M.closure X âŠ† M.closure Y â†” span ğ”½ (v '' X) â‰¤ span ğ”½ (v '' Y) := by
  refine âŸ¨v.span_le_of_closure_subset, fun h e heX â†¦ ?_âŸ©
  rw [v.closure_eq, mem_inter_iff, mem_preimage, SetLike.mem_coe] at heX âŠ¢
  exact âŸ¨h heX.1, heX.2âŸ©

lemma Rep.span_closure_congr (v : M.Rep ğ”½ W) (hXY : M.closure X = M.closure Y) :
    span ğ”½ (v '' X) = span ğ”½ (v '' Y) :=
  (v.span_le_of_closure_subset hXY.subset).antisymm (v.span_le_of_closure_subset hXY.symm.subset)

lemma Rep.span_closure_congr_iff (v : M.Rep ğ”½ W) :
    M.closure X = M.closure Y â†” span ğ”½ (v '' X) = span ğ”½ (v '' Y) :=
  âŸ¨v.span_closure_congr, fun h â†¦ by simp [subset_antisymm_iff, v.closure_subset_iff_span_le, h]âŸ©

@[simp] lemma Rep.span_image_loops (v : M.Rep ğ”½ W) : span ğ”½ (v '' (M.loops)) = âŠ¥ := by
  simp [v.span_closure_congr (M.closure_loops)]

/- If some linear combination of columns of `M.E` is zero, the nonzero indices form a cyclic set.-/
-- lemma Rep.cyclic_of_linearCombination (v : M.Rep ğ”½ W) (c : Î± â†’â‚€ ğ”½)
-- (hcE : (c.support : Set Î±) âŠ† M.E)
--     (hcv : c.linearCombination ğ”½ v = 0) : M.Cyclic c.support := by
--   rw [cyclic_iff_forall_mem_closure_diff_singleton]
--   intro e he
--   rw [v.mem_closure_iff (hcE he), Finsupp.mem_span_image_iff_linearCombination]
--   have hce : c e â‰  0 := by simpa using he
--   use - (c e)â»Â¹ â€¢ (c - Finsupp.single e (c e))
--   suffices âˆ€ (x : Î±), (Â¬c x = 0 â†’ x = e) â†’ c x - (Finsupp.single e (c e)) x = 0 by
--     simpa [Finsupp.mem_supported', hcv, hce, â† smul_assoc]
--   intro x
--   obtain rfl | hne := eq_or_ne x e
--   Â· simp
--   simp +contextual [hne, Finsupp.single_apply_eq_zero]

-- lemma Rep.exists_finsupp_of_isCircuit (v : M.Rep ğ”½ W) {C : Finset Î±} (hC : M.IsCircuit C) :
--     âˆƒ c : Î± â†’â‚€ ğ”½, c.support = C âˆ§ c.linearCombination ğ”½ v = 0 := by
--   have hC' := hC.not_indep
--   rw [v.indep_iff] at hC'
--   obtain âŸ¨c, h, hc, h0âŸ© := linearDepOn_iff.1 hC'
--   refine âŸ¨c, subset_antisymm (by simpa using h) fun e heC â†¦ ?_, hcâŸ©
--   contrapose! h0
--   refine (linearIndepOn_iff.1 <| v.indep_iff.1 <| hC.diff_singleton_indep heC) _ ?_ hc
--   simpa [Finsupp.mem_supported, subset_diff_singleton_iff, h0] using h

lemma Rep.skew_iff_span_disjoint (v : M.Rep ğ”½ W) (hXE : X âŠ† M.E) (hYE : Y âŠ† M.E) :
    M.Skew X Y â†” Disjoint (span ğ”½ (v '' X)) (span ğ”½ (v '' Y)) := by
  obtain âŸ¨I, hIâŸ© := M.exists_isBasis X
  obtain âŸ¨J, hJâŸ© := M.exists_isBasis Y
  rw [â† skew_iff_isBases_skew hI hJ, hI.indep.skew_iff_disjoint_union_indep hJ.indep,
    â† v.span_closure_congr hI.closure_eq_closure, â† v.span_closure_congr hJ.closure_eq_closure,
    v.indep_iff]
  by_cases hdj : Disjoint I J
  Â·  rw [linearIndepOn_union_iff hdj, â† v.indep_iff, â† v.indep_iff, and_iff_right hdj,
      and_iff_right hI.indep, and_iff_right hJ.indep]
  obtain âŸ¨x, hxI, hxJâŸ© := not_disjoint_iff.1 hdj
  simp only [hdj, false_and, disjoint_def, false_iff, not_forall, Classical.not_imp, exists_prop,
    exists_and_left]
  refine âŸ¨v x, (subset_span (mem_image_of_mem v hxI)), (subset_span (mem_image_of_mem v hxJ)), ?_âŸ©
  rw [v.eq_zero_iff_not_indep, not_not]
  exact hI.indep.subset (by simpa)

/-! ### Constructors -/

/-- A function with support contained in `M.E` that gives the correct independent sets
  within the ground set gives a representation -/
@[simps] def Rep.ofGround (v : Î± â†’ W) (h_support : support v âŠ† M.E)
    (h_indep : âˆ€ I âŠ† M.E, M.Indep I â†” LinearIndepOn ğ”½ v I) : M.Rep ğ”½ W where
  to_fun := v
  indep_iff' I := (em (I âŠ† M.E)).elim (h_indep _) fun hI â†¦ iff_of_false (mt Indep.subset_ground hI)
    fun hi â†¦ hI fun _ heI â†¦ h_support <| hi.ne_zero heI

@[simp] lemma Rep.ofGround_apply (f : Î± â†’ W) (hs : support f âŠ† M.E)
  (hf : âˆ€ I âŠ† M.E, (M.Indep I â†” LinearIndependent ğ”½ (I.restrict f))) (a : Î±) :
    Rep.ofGround f hs hf a = f a := rfl

/-- A function from `M.E` to a module determines a representation -/
@[simps!] noncomputable def Rep.ofSubtypeFun (f : M.E â†’ W) [DecidablePred (Â· âˆˆ M.E)]
    (hf : âˆ€ (I : Set M.E), M.Indep (Subtype.val '' I) â†” LinearIndepOn ğ”½ f I) :
    M.Rep ğ”½ W :=
  Rep.ofGround
  ( fun a â†¦ if ha : a âˆˆ M.E then f âŸ¨a,haâŸ© else 0 )
  ( by aesop )
  ( by
    intro I hI
    rw [â† Subtype.range_val (s := M.E), subset_range_iff_exists_image_eq] at hI
    obtain âŸ¨I, rflâŸ© := hI
    rw [hf]
    apply linearIndependent_equiv' <| Equiv.Set.image _ _ Subtype.val_injective
    ext âŸ¨âŸ¨x,hxâŸ©, hx'âŸ©
    simp [dif_pos hx] )

/-- A representation gives a representation of any restriction -/
noncomputable def Rep.restrict (v : M.Rep ğ”½ W) (X : Set Î±) : (M â†¾ X).Rep ğ”½ W :=
  Rep.ofGround (indicator X v) ( by simp )
  ( by
    simp +contextual only [restrict_ground_eq, restrict_indep_iff, v.indep_iff, and_true]
    exact fun I hIX â†¦ linearIndepOn_congr <| eqOn_indicator.symm.mono hIX )

@[simp] lemma Rep.restrict_apply (v : M.Rep ğ”½ W) (X : Set Î±) :
    (v.restrict X : Î± â†’ W) = indicator X v := rfl

section Simple

@[simp]
lemma Rep.ne_zero [M.Loopless] [M.OnUniv] (v : M.Rep ğ”½ W) (e : Î±) : v e â‰  0 := by
  simp [v.ne_zero_iff_isNonloop]

lemma Rep.loopless_iff (v : M.Rep ğ”½ W) : M.Loopless â†” âˆ€ e âˆˆ M.E, v e â‰  0 := by
  rw [loopless_iff_forall_isNonloop]
  exact âŸ¨fun h e he â†¦ (v.ne_zero_iff_isNonloop e).2 (h e he),
    fun h e he â†¦ (v.ne_zero_iff_isNonloop e).1 (h e he)âŸ©

lemma Rep.parallel_iff (v : M.Rep ğ”½ W) (he : M.IsNonloop e) :
    M.Parallel e f â†” âˆƒ (c : ğ”½), c â‰  0 âˆ§ c â€¢ v f = v e := by
  rw [he.parallel_iff_mem_closure, v.closure_eq]
  simp only [image_singleton, mem_inter_iff, mem_preimage, SetLike.mem_coe, he.mem_ground, and_true,
    ne_eq, mem_span_singleton]
  refine âŸ¨fun âŸ¨a, haâŸ© â†¦ âŸ¨a, fun ha0 â†¦ v.ne_zero_of_isNonloop he ?_, haâŸ©, fun âŸ¨c, hc0, hcâŸ© â†¦ âŸ¨c, hcâŸ©âŸ©
  rw [â† ha, ha0, zero_smul]

lemma Rep.parallel_iff' (v : M.Rep ğ”½ W) (he : M.IsNonloop e) :
    M.Parallel e f â†” âˆƒ (c : ğ”½Ë£), c â€¢ v f = v e := by
  rw [v.parallel_iff he]
  exact âŸ¨fun âŸ¨c, hne, heqâŸ© â†¦ âŸ¨Units.mk0 c hne, by simpaâŸ©, fun âŸ¨c, heqâŸ© â†¦ âŸ¨c, by simp, heqâŸ©âŸ©

lemma Rep.simple_iff [RankPos M] (v : M.Rep ğ”½ W) :
    M.Simple â†” âˆ€ {e f} (_ : e âˆˆ M.E) (_ : f âˆˆ M.E) (c : ğ”½), c â€¢ (v f) = v e â†’ e = f := by
  simp_rw [simple_iff_isLoopless_eq_of_parallel_forall, v.loopless_iff]
  refine âŸ¨fun âŸ¨h0,h1âŸ© e f he _ c h_eq â†¦ h1 e f ?_, fun h â†¦ âŸ¨fun e he h0 â†¦ ?_, fun e f hef â†¦ ?_âŸ©âŸ©
  Â· refine (v.parallel_iff ?_).2 âŸ¨c, ?_, h_eqâŸ©
    Â· rw [â† v.ne_zero_iff_isNonloop e]; exact h0 _ he
    rintro rfl
    exact h0 e he <| by simp [â† h_eq]
  Â· obtain âŸ¨f, hfâŸ© := M.exists_isNonloop
    obtain rfl := h he hf.mem_ground 0 (by simp [h0])
    exact v.ne_zero_of_isNonloop hf h0
  obtain âŸ¨c,-,h_eqâŸ© := (v.parallel_iff hef.symm.isNonloop_right).1 hef
  exact h (by aesop_mat) (by aesop_mat) c h_eq

lemma Rep.injOn_of_simple (v : M.Rep ğ”½ W) [h : M.Simple] : InjOn v M.E := by
  obtain (hl | hpos) := M.eq_loopyOn_or_rankPos
  Â· rw [simple_iff_isLoopless_eq_of_parallel_forall, hl, loopyOn_isLoopless_iff] at h
    simp [h.1]
  exact fun e he f hf h_eq â†¦ (v.simple_iff.1 h) he hf 1 <| by rwa [one_smul, eq_comm]




-- @[simp] lemma simplification_representable_iff :
--     M.simplification.Representable ğ”½ â†” M.Representable ğ”½ := by
--   obtain âŸ¨c, hc, hMâŸ© := M.exists_simplification_eq_wrt
--   rw [hM]
--   refine âŸ¨fun âŸ¨vâŸ© â†¦ ?_, fun h â†¦ h.minor (simplificationWrt_isRestriction hc).minorâŸ©
--   rw [â† removeLoops_representable_iff, â† preimage_simplificationWrt M hc]
--   exact (v.preimage _).representable

end Simple
