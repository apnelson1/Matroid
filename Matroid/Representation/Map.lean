import Mathlib.LinearAlgebra.FiniteDimensional.Defs
import Mathlib.LinearAlgebra.Dimension.Constructions
import Matroid.Representation.Basic

variable {Î± Î² W W' ğ”½ R : Type*} {e f x : Î±} {I E B X Y : Set Î±} {M : Matroid Î±} [DivisionRing ğ”½]
  [AddCommGroup W] [Module ğ”½ W] [AddCommGroup W'] [Module ğ”½ W']

open Function Set Submodule FiniteDimensional BigOperators Matrix Set.Notation Module

namespace Matroid

/-- Compose a representation `v` with a linear map that is injective on the range of `v`-/
def Rep.comp (v : M.Rep ğ”½ W) (Ïˆ : W â†’â‚—[ğ”½] W')
    (h_inj : Disjoint (span ğ”½ (range v)) (LinearMap.ker Ïˆ)) : M.Rep ğ”½ W' where
  to_fun := Ïˆ âˆ˜ v
  indep_iff' := fun I â†¦ by
    rw [LinearMap.linearIndepOn_iff_of_injOn, v.indep_iff]
    exact LinearMap.injOn_of_disjoint_ker (span_mono <| image_subset_range ..) h_inj

/-! ### Maps between representations -/

/-- Compose a representation with a linear injection. -/
def Rep.comp' (v : M.Rep ğ”½ W) (Ïˆ : W â†’â‚—[ğ”½] W') (hÏˆ : LinearMap.ker Ïˆ = âŠ¥) := v.comp Ïˆ (by simp [hÏˆ])

/-- Compose a representation with a linear equivalence. -/
def Rep.compEquiv (v : M.Rep ğ”½ W) (Ïˆ : W â‰ƒâ‚—[ğ”½] W') : M.Rep ğ”½ W' := v.comp' Ïˆ Ïˆ.ker

@[simp] lemma Rep.comp_apply (v : M.Rep ğ”½ W) (Ïˆ : W â†’â‚—[ğ”½] W') (h_inj) (e : Î±) :
    (v.comp Ïˆ h_inj) e = Ïˆ (v e) := rfl

@[simp] lemma Rep.comp_coeFun (v : M.Rep ğ”½ W) (Ïˆ : W â†’â‚—[ğ”½] W') (h_inj) :
    (v.comp Ïˆ h_inj) = Ïˆ âˆ˜ v := rfl

@[simp] lemma Rep.comp'_apply (v : M.Rep ğ”½ W) (Ïˆ : W â†’â‚—[ğ”½] W') (hÏˆ : LinearMap.ker Ïˆ = âŠ¥) (e : Î±) :
    (v.comp' Ïˆ hÏˆ) e = Ïˆ (v e) := rfl

@[simp] lemma Rep.comp'_coeFun (v : M.Rep ğ”½ W) (Ïˆ : W â†’â‚—[ğ”½] W') (h_inj) :
    (v.comp' Ïˆ h_inj) = Ïˆ âˆ˜ v := rfl

@[simp] lemma Rep.compEquiv_apply (v : M.Rep ğ”½ W) (Ïˆ : W â‰ƒâ‚—[ğ”½] W') (e : Î±) :
    (v.compEquiv Ïˆ) e = Ïˆ (v e) := rfl

@[simp] lemma Rep.compEquiv_coeFun (v : M.Rep ğ”½ W) (Ïˆ : W â‰ƒâ‚—[ğ”½] W') :
    v.compEquiv Ïˆ = Ïˆ âˆ˜ v := rfl

@[simp] lemma Rep.compEquiv_compEquiv_symm (v : M.Rep ğ”½ W) (Ïˆ : W â‰ƒâ‚—[ğ”½] W') :
    (v.compEquiv Ïˆ).compEquiv Ïˆ.symm = v := by
  ext
  simp

@[simp] lemma Rep.compEquiv_symm_compEquiv (v : M.Rep ğ”½ W') (Ïˆ : W â‰ƒâ‚—[ğ”½] W') :
    (v.compEquiv Ïˆ.symm).compEquiv Ïˆ = v := by
  ext
  simp

/--  Compose a representation with a module equality -/
def Rep.subtype_ofEq {Wâ‚ Wâ‚‚ : Submodule ğ”½ W} (v : M.Rep ğ”½ Wâ‚) (h : Wâ‚ = Wâ‚‚) : M.Rep ğ”½ Wâ‚‚ :=
  v.compEquiv <| LinearEquiv.ofEq _ _ h

@[simp] lemma Rep.subtype_ofEq_apply {Wâ‚ Wâ‚‚ : Submodule ğ”½ W} (v : M.Rep ğ”½ Wâ‚) (h : Wâ‚ = Wâ‚‚)
    (e : Î±) : v.subtype_ofEq h e = LinearEquiv.ofEq _ _ h (v e) := rfl

@[simps!] def Rep.finsuppToFun (v : M.Rep ğ”½ (Î² â†’â‚€ ğ”½)) : M.Rep ğ”½ (Î² â†’ ğ”½) :=
  v.comp' Finsupp.lcoeFun (by simp)

/-- A representation gives a representation of a comap -/
@[simps] def Rep.comap {M : Matroid Î²} (v : M.Rep ğ”½ W) (f : Î± â†’ Î²) : (M.comap f).Rep ğ”½ W where
  to_fun := v âˆ˜ f
  indep_iff' I := by
    simp only [comap_indep_iff, v.indep_iff]
    exact âŸ¨fun h â†¦ h.1.comp_of_image h.2, fun h â†¦ âŸ¨h.image_of_comp, h.injOn.of_compâŸ©âŸ©

lemma Rep.comap_coeFun_eq {M : Matroid Î²} (f : Î± â†’ Î²) (v : M.Rep ğ”½ W) :
    (v.comap f : Î± â†’ W) = v âˆ˜ f := rfl

@[simp] lemma Rep.comap_apply {M : Matroid Î²} (f : Î± â†’ Î²) (v : M.Rep ğ”½ W) (a : Î±) :
    v.comap f a = v (f a) := rfl

@[simps] def Rep.ofEq {M N : Matroid Î±} (v : M.Rep ğ”½ W) (h : M = N) : N.Rep ğ”½ W where
  to_fun := v
  indep_iff' := by simp [â† v.indep_iff, h]

@[simp] lemma Rep.ofEq_apply {M N : Matroid Î±} (v : M.Rep ğ”½ W) (h : M = N) :
  (v.ofEq h : Î± â†’ W) = v := rfl

noncomputable def Rep.restrictSubtype (v : M.Rep ğ”½ W) (X : Set Î±) : (M.restrictSubtype X).Rep ğ”½ W :=
  (v.restrict X).comap ((â†‘) : X â†’ Î±)

/-- Transfer a `Rep` along a matroid map. The definition involves extending a function with zero,
so requires a `DecidablePred` assumption. -/
noncomputable def Rep.map (v : M.Rep ğ”½ W) (f : Î± â†’ Î²) (hf : M.E.InjOn f)
    [DecidablePred (âˆƒ y âˆˆ M.E, f y = Â·)] : (M.map f hf).Rep ğ”½ W :=
  let v' := fun (x : Î²) â†¦ if h : âˆƒ y âˆˆ M.E, f y = x then v h.choose else 0
  have h_eq : EqOn (v' âˆ˜ f) v M.E := by
    intro x hx
    have h : âˆƒ y âˆˆ M.E, f y = f x := âŸ¨x, hx, rflâŸ©
    simp [v', dif_pos h, show h.choose = x from hf h.choose_spec.1 hx h.choose_spec.2]
  Rep.ofGround
  ( v := v')
  ( h_support := fun x â†¦ by
      simp only [mem_support, map_ground, v']
      split_ifs with h
      Â· exact fun hne â†¦ âŸ¨_, v.support_subset_ground hne, h.choose_spec.2 âŸ©
      simp )
  ( h_indep := by
      simp only [map_ground, map_indep_iff, forall_subset_image_iff]
      refine fun I hIE â†¦ âŸ¨fun âŸ¨I', hI', hII'âŸ© â†¦ ?_, fun h â†¦ âŸ¨_, ?_, rflâŸ©âŸ©
      Â· rw [hII']
        exact ((v.onIndep hI').congr <| h_eq.symm.mono hI'.subset_ground).image_of_comp
      exact v.indep_iff.2 <| (h.comp_of_image (hf.mono hIE)).congr (h_eq.mono hIE) )

lemma Rep.matroidMap_apply (v : M.Rep ğ”½ W) {f : Î± â†’ Î²} {hf} [DecidablePred (âˆƒ y âˆˆ M.E, f y = Â·)]
    {x : Î±} (hx : x âˆˆ M.E) : v.map f hf (f x) = v x := by
  have h : âˆƒ y âˆˆ M.E, f y = f x := âŸ¨x, hx, rflâŸ©
  simp [map, dif_pos h, show h.choose = x from hf h.choose_spec.1 hx h.choose_spec.2]

lemma Rep.matroidMap_image (v : M.Rep ğ”½ W) (f : Î± â†’ Î²) (hf) [DecidablePred (âˆƒ y âˆˆ M.E, f y = Â·)]
    (hX : X âŠ† M.E) : v.map f hf '' (f '' X) = v '' X := by
  ext x
  simp only [mem_image, exists_exists_and_eq_and]
  constructor <;>
  Â· rintro âŸ¨a, ha, rflâŸ©
    exact âŸ¨a, ha, by rw [v.matroidMap_apply (hX ha)]âŸ©

/-- Scale a representation by an invertible scalar for each element of `Î±`. -/
@[simps] def Rep.scale (v : M.Rep ğ”½ W) (c : Î± â†’ ğ”½Ë£) : M.Rep ğ”½ W where
  to_fun := c â€¢ v
  indep_iff' I := by
    rw [v.indep_iff]
    exact (LinearIndependent.units_smul_iff ..).symm

/-- The `ğ”½`-representable matroid whose ground set is a vector space `W` over `ğ”½`,
and independence is linear independence.  -/
protected def _root_.Module.matroid (ğ”½ W : Type*) [AddCommGroup W] [DivisionRing ğ”½] [Module ğ”½ W] :
    Matroid W :=
  IndepMatroid.matroid <| IndepMatroid.ofFinitaryCardAugment
  (E := univ)
  (Indep := fun I â†¦ LinearIndepOn ğ”½ id I)
  (indep_empty := linearIndependent_empty _ _)
  (indep_subset := fun I J hJ hIJ â†¦ hJ.mono hIJ)
  (indep_aug := by
    intro I J hI hIfin hJ hJfin hIJ
    have hssu : Â¬ (J âŠ† span ğ”½ I) := by
      rw [â† span_le]
      refine fun hss â†¦ hIJ.not_ge ?_
      have _ := hIfin.fintype
      have _ := hJfin.fintype
      have _ : Module.Finite ğ”½ (span ğ”½ I) := FiniteDimensional.span_of_finite _ hIfin
      rw [ncard_eq_toFinset_card' J, ncard_eq_toFinset_card' I, â† finrank_span_set_eq_card hJ,
        â† finrank_span_set_eq_card hI]
      exact Submodule.finrank_mono hss
    obtain âŸ¨a, haJ, haâŸ© := not_subset.1 hssu
    refine âŸ¨a, haJ, notMem_subset subset_span ha, ?_âŸ©
    simp only [SetLike.mem_coe] at ha
    simpa [linearIndepOn_insert (notMem_subset subset_span ha), ha])
  (indep_compact := linearIndepOn_of_finite)
  (subset_ground := by simp)

@[simps!]
protected def _root_.Module.matroidRep (ğ”½ W : Type*) [AddCommGroup W] [DivisionRing ğ”½]
    [Module ğ”½ W] : (Module.matroid ğ”½ W).Rep ğ”½ W where
  to_fun := id
  indep_iff' _ := by rfl

@[simp]
protected lemma _root_.Module.matroid_subsingleton (ğ”½ W : Type*) [AddCommGroup W] [DivisionRing ğ”½]
    [Module ğ”½ W] [Subsingleton W] : Module.matroid ğ”½ W = loopyOn {0} := by
  simp [eq_loopyOn_iff, Module.matroid, Set.ext_iff, Subsingleton.eq_zero]

instance _root_.Module.matroid_finitary : Finitary (Module.matroid ğ”½ W) := by
  rw [Module.matroid]
  infer_instance

lemma Rep.eq_comap (v : M.Rep ğ”½ W) : M = (_root_.Module.matroid ğ”½ W).comapOn M.E v := by
  refine ext_indep rfl fun I hI â†¦ ?_
  simp only [v.indep_iff, Module.matroid, comapOn_indep_iff, IndepMatroid.matroid_Indep,
    IndepMatroid.ofFinitaryCardAugment_indep, hI, and_true]
  rw [LinearIndepOn_iff_linearIndepOn_image_injOn]

lemma Rep.finitary (v : M.Rep ğ”½ W) : M.Finitary := by
  rw [v.eq_comap]
  exact comapOn_finitary

/-! ### Representations from functions -/

/-- The `ğ”½`-representable matroid given by a function `f : Î± â†’ W` for a vector space `W` over `ğ”½`,
and a ground set `E : Set Î±`.  -/
protected def ofFun (ğ”½ : Type*) [DivisionRing ğ”½] [Module ğ”½ W] (E : Set Î±) (f : Î± â†’ W) : Matroid Î± :=
  (Module.matroid ğ”½ W).comapOn E f

lemma ofFun_congr {v v' : Î± â†’ W} (hvv' : EqOn v v' E) :
    Matroid.ofFun ğ”½ E v = Matroid.ofFun ğ”½ E v' := by
  refine ext_indep rfl fun I (hI : I âŠ† E) â†¦ ?_
  simp [Matroid.ofFun, (hvv'.mono hI).image_eq, (hvv'.mono hI).injOn_iff]

@[simp] lemma ofFun_indicator {v : Î± â†’ W} :
    Matroid.ofFun ğ”½ E (E.indicator v) = Matroid.ofFun ğ”½ E v :=
  ofFun_congr <| eqOn_indicator

noncomputable def repOfFun (ğ”½ : Type*) [DivisionRing ğ”½] [Module ğ”½ W] (E : Set Î±) (f : Î± â†’ W) :
    (Matroid.ofFun ğ”½ E f).Rep ğ”½ W :=
  ((Module.matroidRep ğ”½ W).comap f).restrict E

lemma repOfFun_coeFun_eq (ğ”½ : Type*) [DivisionRing ğ”½] [Module ğ”½ W] (E : Set Î±) (f : Î± â†’ W) :
    (repOfFun ğ”½ E f : Î± â†’ W) = indicator E f := rfl

lemma repOfFun_coeFun_eq' (ğ”½ : Type*) [DivisionRing ğ”½] [Module ğ”½ W] (E : Set Î±) (f : Î± â†’ W)
    (hf : support f âŠ† E) : (repOfFun ğ”½ E f : Î± â†’ W) = f := by
  rwa [repOfFun_coeFun_eq, indicator_eq_self]

@[simp] lemma repOfFun_image_eq (ğ”½ : Type*) [DivisionRing ğ”½] [Module ğ”½ W] (E : Set Î±) (f : Î± â†’ W) :
    (repOfFun ğ”½ E f '' E) = f '' E := by
  rw [repOfFun_coeFun_eq]
  aesop

lemma repOfFun_apply (ğ”½ : Type*) [DivisionRing ğ”½] [Module ğ”½ W] {v : Î± â†’ W} (he : e âˆˆ E) :
    (repOfFun ğ”½ E v) e = v e := by
  change indicator E v e = v e
  simp [he]

instance matroidOfFun_finitary (ğ”½ : Type*) [DivisionRing ğ”½] [Module ğ”½ W] (f : Î± â†’ W) (E : Set Î±) :
    Finitary (Matroid.ofFun ğ”½ E f) := by
  rw [Matroid.ofFun, comapOn]
  infer_instance

lemma ofFun_finite (f : Î± â†’ W) (E : Set Î±) (hfin : E.Finite) : (Matroid.ofFun ğ”½ E f).Finite :=
  âŸ¨hfinâŸ©

@[simp] lemma ofFun_ground_eq {v : Î± â†’ W} {E : Set Î±} : (Matroid.ofFun ğ”½ E v).E = E := rfl

@[simp] lemma ofFun_indep_iff {v : Î± â†’ W} {E : Set Î±} :
    (Matroid.ofFun ğ”½ E v).Indep I â†” LinearIndepOn ğ”½ v I âˆ§ I âŠ† E := by
  rw [Matroid.ofFun, comapOn_indep_iff]
  by_cases hinj : InjOn v I
  Â· simp only [Module.matroid, IndepMatroid.matroid_Indep, and_iff_right hinj,
    IndepMatroid.ofFinitaryCardAugment_indep, â† linearIndepOn_iff_image hinj]
  exact iff_of_false (by simp [hinj]) fun hli â†¦ hinj <| injOn_iff_injective.2 hli.1.injective

@[simp] lemma Rep.ofFun_self (v : M.Rep ğ”½ W) : Matroid.ofFun ğ”½ M.E v = M :=
  ext_indep rfl fun I (hIE : I âŠ† M.E) â†¦ by rw [ofFun_indep_iff, â† v.indep_iff, and_iff_left hIE]

lemma ofFun_closure_eq {v : Î± â†’ W} {E : Set Î±} (hvE : support v âŠ† E) :
    (Matroid.ofFun ğ”½ E v).closure X = v â»Â¹' (span ğ”½ (v '' X)) âˆ© E := by
  rw [(repOfFun ğ”½ E v).closure_eq, repOfFun_coeFun_eq, ofFun_ground_eq, indicator_preimage]
  simp +contextual [indicator_eq_self.2 hvE]

lemma ofFun_closure_eq_of_subset_ground {v : Î± â†’ W} {E : Set Î±} (hXE : X âŠ† E) :
    (Matroid.ofFun ğ”½ E v).closure X = v â»Â¹' (span ğ”½ (v '' X)) âˆ© E := by
  rw [â† ofFun_indicator, ofFun_closure_eq (by simp), indicator_preimage,
    ((Set.eqOn_indicator (f := v)).mono hXE).image_eq]
  simp

lemma _root_.Module.Basis.ofFun_isBase {v : Î± â†’ W} {E : Set Î±} {B : Set Î±} (b : Module.Basis B ğ”½ W)
    (hfb : âˆ€ x : B, v x = b x) (hBE : B âŠ† E) : (Matroid.ofFun ğ”½ E v).IsBase B := by
  have hrw : v '' B = range b := by simp_rw [Set.ext_iff, mem_range, â† hfb]; aesop

  refine Indep.isBase_of_ground_subset_closure ?_ ?_
  Â· simp only [ofFun_indep_iff, LinearIndepOn, hfb, and_iff_left hBE]
    exact b.linearIndependent
  rw [ofFun_closure_eq_of_subset_ground hBE, hrw, b.span_eq]
  simp

@[simp] lemma ofFun_zero (ğ”½ : Type*) [Field ğ”½] [Module ğ”½ W] (E : Set Î±) :
    (Matroid.ofFun ğ”½ E (0 : Î± â†’ W)) = loopyOn E := by
  simp +contextual [eq_loopyOn_iff]
