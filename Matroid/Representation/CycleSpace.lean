import Matroid.Representation.Minor

variable {Î± Î² W W' ğ”½ R : Type*} {e f x : Î±} {I C E B X Y : Set Î±} {M : Matroid Î±} [DivisionRing ğ”½]
  [AddCommGroup W] [Module ğ”½ W] [AddCommGroup W'] [Module ğ”½ W'] {c : Î± â†’â‚€ ğ”½}

open Set Function Submodule

namespace Matroid

/-- The `cycleSpace` of an `ğ”½`-representation `v` of `M : Matroid Î±` is the set of vectors
in `Î± â†’â‚€ ğ”½` that are supported on a finite subset of `M.E` and give a linear combination of
zero with the elements of `v`. This space contains a vector supported on every circuit.
This is the right null space of a matrix representation of `M`. -/
noncomputable def Rep.cycleSpace (v : M.Rep ğ”½ W) :=
    (LinearMap.ker (Finsupp.linearCombination ğ”½ v) âŠ“ Finsupp.supported ğ”½ ğ”½ M.E)

lemma Rep.supported_finiteDimensional [M.Finite] (_ : M.Rep ğ”½ W) :
    FiniteDimensional ğ”½ <| Finsupp.supported ğ”½ ğ”½ M.E :=
  have := M.ground_finite.to_subtype
  Module.Finite.equiv (Finsupp.supportedEquivFinsupp M.E).symm

lemma Rep.cycleSpace_finiteDimensional [M.Finite] (v : M.Rep ğ”½ W) :
    FiniteDimensional ğ”½ v.cycleSpace := by
  have := v.supported_finiteDimensional
  exact Submodule.finiteDimensional_inf_right ..

lemma Rep.mem_cycleSpace_iff (v : M.Rep ğ”½ W) :
    c âˆˆ v.cycleSpace â†” c.linearCombination ğ”½ v = 0 âˆ§ support c âŠ† M.E := by
  simp [Rep.cycleSpace, Finsupp.mem_supported]

lemma Rep.support_subset_ground_of_mem_cycleSpace (v : M.Rep ğ”½ W) (hc : c âˆˆ v.cycleSpace) :
    support c âŠ† M.E :=
  (v.mem_cycleSpace_iff.1 hc).2

@[simp]
lemma Rep.cycleSpace_comp (v : M.Rep ğ”½ W) (Ï† : W â†’â‚—[ğ”½] W') (hÏ†) :
    (v.comp Ï† hÏ†).cycleSpace = v.cycleSpace := by
  rw [disjoint_def] at hÏ†
  ext x
  simp only [mem_cycleSpace_iff, comp_coeFun, Finsupp.fun_support_eq, and_congr_left_iff]
  rw [â† LinearMap.map_finsupp_linearCombination, â† LinearMap.mem_ker]
  refine fun h â†¦ âŸ¨fun h0 â†¦ hÏ† _ ?_ h0, fun h0 â†¦ by simp [h0]âŸ©
  rw [â† image_univ, Finsupp.mem_span_image_iff_linearCombination]
  exact âŸ¨x, by simp, rflâŸ©

@[simp]
lemma Rep.cycleSpace_comp' (v : M.Rep ğ”½ W) (Ï† : W â†’â‚—[ğ”½] W') (hÏ†) :
    (v.comp' Ï† hÏ†).cycleSpace = v.cycleSpace := by
  simp [comp']

@[simp]
lemma Rep.cycleSpace_compEquiv (v : M.Rep ğ”½ W) (Ï† : W â‰ƒâ‚—[ğ”½] W') :
    (v.compEquiv Ï†).cycleSpace = v.cycleSpace := by
  simp [compEquiv]

@[simp]
lemma Rep.restrictSpan_cycleSpace (v : M.Rep ğ”½ W) :
    v.restrictSpan.cycleSpace = v.cycleSpace := by
  simpa using congr_arg Rep.cycleSpace v.restrictSpan_comp

@[simp]
lemma Rep.standardRep'_cycleSpace (v : M.Rep ğ”½ W) (hB : M.IsBase B) :
    (v.standardRep' hB).cycleSpace = v.cycleSpace := by
  simp [standardRep']

@[simp]
lemma Rep.standardRep_cycleSpace (v : M.Rep ğ”½ W) (hB : M.IsBase B) :
    (v.standardRep hB).cycleSpace = v.cycleSpace := by
  simp [standardRep]

/-- If some linear combination of columns of `M.E` is zero, the nonzero indices form a cyclic set.-/
lemma Rep.cyclic_of_linearCombination (v : M.Rep ğ”½ W) (c : Î± â†’â‚€ ğ”½) (hcE : (c.support : Set Î±) âŠ† M.E)
    (hcv : c.linearCombination ğ”½ v = 0) : M.Cyclic c.support := by
  rw [cyclic_iff_forall_mem_closure_diff_singleton]
  intro e he
  rw [v.mem_closure_iff (hcE he), Finsupp.mem_span_image_iff_linearCombination]
  have hce : c e â‰  0 := by simpa using he
  use - (c e)â»Â¹ â€¢ (c - Finsupp.single e (c e))
  suffices âˆ€ (x : Î±), (Â¬c x = 0 â†’ x = e) â†’ c x - (Finsupp.single e (c e)) x = 0 by
    simpa [Finsupp.mem_supported', hcv, hce, â† smul_assoc]
  intro x
  obtain rfl | hne := eq_or_ne x e
  Â· simp
  simp +contextual [hne, Finsupp.single_apply_eq_zero]

lemma Rep.support_cyclic_of_mem_cycleSpace (v : M.Rep ğ”½ W) {c : Î± â†’â‚€ ğ”½} (hc : c âˆˆ v.cycleSpace) :
    M.Cyclic c.support := by
  simp only [cyclic_iff_forall_mem_closure_diff_singleton, Finset.mem_coe, Finsupp.mem_support_iff,
    ne_eq, v.mem_closure_iff', Finsupp.mem_span_image_iff_linearCombination, Finsupp.mem_supported]
  rw [mem_cycleSpace_iff] at hc
  refine fun e he â†¦ âŸ¨âŸ¨- (c e)â»Â¹ â€¢ (c - Finsupp.single e (c e)), fun x â†¦ ?_,
    by simp [hc.1, inv_smul_smulâ‚€ he]âŸ©, hc.2 (by simpa)âŸ©
  obtain rfl | hne := eq_or_ne x e
  Â· simp
  simp [he, hne, sub_eq_zero, Finsupp.single_eq_of_ne hne.symm]

lemma Rep.exists_finsupp_of_isCircuit (v : M.Rep ğ”½ W) (hC : M.IsCircuit C) :
    âˆƒ c : Î± â†’â‚€ ğ”½, c.support = C âˆ§ c.linearCombination ğ”½ v = 0 := by
  have hC' := hC.not_indep
  rw [v.indep_iff, linearDepOn_iff'] at hC'
  obtain âŸ¨c, hcsupp, hc, hc0âŸ© := hC'
  refine âŸ¨c, subset_antisymm (by simpa using hcsupp) fun e heC â†¦ ?_, hcâŸ©
  simp only [Finset.mem_coe, Finsupp.mem_support_iff, ne_eq]
  refine fun hc' â†¦ hc0 <| (linearIndepOn_iff.1 <| v.onIndep <| hC.diff_singleton_indep heC) c ?_ hc
  simpa [Finsupp.mem_supported, subset_diff_singleton_iff, hc']

lemma Rep.exists_mem_cycleSpace_of_isCircuit (v : M.Rep ğ”½ W) (hC : M.IsCircuit C) :
    âˆƒ w âˆˆ v.cycleSpace, w.support = C := by
  obtain âŸ¨c, rfl, hcâŸ© := v.exists_finsupp_of_isCircuit hC
  exact âŸ¨c, âŸ¨hc, by simp [Finsupp.mem_supported, hC.subset_ground]âŸ©, rflâŸ©

variable {ğ”½ W : Type*} [Field ğ”½] [AddCommGroup W] [Module ğ”½ W] [Module ğ”½ W']

-- These next two definitions are presumably somewhere in mathlib.
@[simps] noncomputable def myLinMap : Module.Dual ğ”½ (Î± â†’â‚€ ğ”½) â†’â‚—[ğ”½] (Î± â†’ ğ”½) where
  toFun Ï† a := Ï† (Finsupp.single a 1)
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

def mySupported (ğ”½ : Type*) [Field ğ”½] (s : Set Î±) : Submodule ğ”½ (Î± â†’ ğ”½) where
  carrier := {f | âˆ€ i âˆ‰ s, f i = 0}
  add_mem' := @fun f g hf hg i his â†¦ by simp [hf i his, hg i his]
  zero_mem' := by simp
  smul_mem' := @fun c x hc i his â†¦ by simp [hc i his]

@[simp]
lemma mem_mySupported_iff {s : Set Î±} {x : Î± â†’ ğ”½} :
    x âˆˆ mySupported ğ”½ s â†” support x âŠ† s := by
  simp [mySupported, not_imp_comm]

/-- The `cocycleSpace` of an `ğ”½`-representation of `M : Matroid Î±` is the set of vectors
in `Î± â†’ ğ”½` that are supported on `M.E`, and are orthogonal to every vector in the `cycleSpace`.
This corresponds to the 'row space' of a matrix representation.  -/
noncomputable def Rep.cocycleSpace (v : M.Rep ğ”½ W) : Submodule ğ”½ (Î± â†’ ğ”½) :=
  ((dualAnnihilator v.cycleSpace).map myLinMap âŠ“ mySupported ğ”½ M.E)

@[simp]
lemma Rep.cocycleSpace_comp (v : M.Rep ğ”½ W) (Ï† : W â†’â‚—[ğ”½] W') (hÏ†) :
    (v.comp Ï† hÏ†).cocycleSpace = v.cocycleSpace := by
  simp [cocycleSpace]

@[simp]
lemma Rep.cocycleSpace_comp' (v : M.Rep ğ”½ W) (Ï† : W â†’â‚—[ğ”½] W') (hÏ†) :
    (v.comp' Ï† hÏ†).cocycleSpace = v.cocycleSpace := by
  simp [comp']

@[simp]
lemma Rep.cocycleSpace_compEquiv (v : M.Rep ğ”½ W) (Ï† : W â‰ƒâ‚—[ğ”½] W') :
    (v.compEquiv Ï†).cocycleSpace = v.cocycleSpace := by
  simp [compEquiv]

@[simp]
lemma Rep.restrictSpan_cocycleSpace (v : M.Rep ğ”½ W) :
    v.restrictSpan.cocycleSpace = v.cocycleSpace := by
  simp [cocycleSpace]

@[simp]
lemma Rep.standardRep'_cocycleSpace (v : M.Rep ğ”½ W) (hB : M.IsBase B) :
    (v.standardRep' hB).cocycleSpace = v.cocycleSpace := by
  simp [cocycleSpace]

@[simp]
lemma Rep.standardRep_cocycleSpace (v : M.Rep ğ”½ W) (hB : M.IsBase B) :
    (v.standardRep hB).cocycleSpace = v.cocycleSpace := by
  simp [cocycleSpace]

lemma Rep.mem_cocycleSpace_iff_of_support (v : M.Rep ğ”½ W) {x : Î± â†’ ğ”½} (hx : support x âŠ† M.E) :
    x âˆˆ v.cocycleSpace â†” âˆ€ y âˆˆ v.cycleSpace, Finsupp.linearCombination ğ”½ x y = 0 := by
  simp only [Rep.cocycleSpace, myLinMap, mem_inf, mem_map, mem_dualAnnihilator,
    Rep.mem_cycleSpace_iff, and_imp, LinearMap.coe_mk, AddHom.coe_mk, mem_mySupported_iff,
     ne_eq, not_imp_comm]
  refine âŸ¨fun h y hy hyE â†¦ ?_, fun h â†¦ âŸ¨âŸ¨_, h, by simpâŸ©, hxâŸ©âŸ©
  obtain âŸ¨âŸ¨z,hz, rflâŸ©, hsuppâŸ© := h
  rw [â† hz y hy hyE]
  convert (z.map_finsupp_linearCombination (g := fun a : Î± â†¦ Finsupp.single a 1) y).symm
  simp [Finsupp.linearCombination]

lemma Rep.mem_cocycleSpace_iff (v : M.Rep ğ”½ W) {x : Î± â†’ ğ”½} :
    x âˆˆ v.cocycleSpace â†”
      (âˆ€ y âˆˆ v.cycleSpace, Finsupp.linearCombination ğ”½ x y = 0) âˆ§ support x âŠ† M.E := by
  by_cases hsupp : support x âŠ† M.E
  Â· rw [mem_cocycleSpace_iff_of_support v hsupp, and_iff_left hsupp]
  simp [Rep.cocycleSpace, hsupp]

lemma Rep.mem_cycleSpace_iff_forall_of_support (v : M.Rep ğ”½ W) {y : Î± â†’â‚€ ğ”½} (hy : support y âŠ† M.E) :
    y âˆˆ v.cycleSpace â†” âˆ€ x âˆˆ v.cocycleSpace, Finsupp.linearCombination ğ”½ x y = 0 := by
  obtain âŸ¨B, hBâŸ© := M.exists_isBase
  rw [â† v.standardRep_cycleSpace hB, â† v.standardRep_cocycleSpace hB]
  set v' := v.standardRep hB
  simp +contextual only [mem_cocycleSpace_iff, ne_eq, and_imp]
  refine âŸ¨fun hy x h hx â†¦ h y hy, fun h â†¦ ?_âŸ©
  rw [mem_cycleSpace_iff, and_iff_left hy]
  ext i
  specialize h (v' Â· i) (fun y hy â†¦ ?_)   ?_
  Â· simpa [Finsupp.linearCombination, Finsupp.sum] using congr_fun (v'.mem_cycleSpace_iff.1 hy).1 i
  Â· exact subset_trans (by simp +contextual [not_imp_not]) v'.support_subset_ground
  simpa [Finsupp.linearCombination, Finsupp.sum] using h

lemma Rep.mem_cycleSpace_iff_forall (v : M.Rep ğ”½ W) {y : Î± â†’â‚€ ğ”½} :
    y âˆˆ v.cycleSpace â†”
      (âˆ€ x âˆˆ v.cocycleSpace, Finsupp.linearCombination ğ”½ x y = 0) âˆ§ support y âŠ† M.E  := by
  by_cases hsupp : support y âŠ† M.E
  Â· rw [mem_cycleSpace_iff_forall_of_support _ hsupp, and_iff_left hsupp]
  simp only [mem_cycleSpace_iff, and_congr_left_iff, hsupp, false_imp_iff]
