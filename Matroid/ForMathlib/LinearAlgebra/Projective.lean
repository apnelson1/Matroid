import Mathlib.LinearAlgebra.Projectivization.Independence
import Matroid.ForMathlib.LinearAlgebra.LinearIndependent
import Matroid.ForMathlib.LinearAlgebra.Submodule

variable {Î¹ K V : Type*} [DivisionRing K] [AddCommGroup V] [Module K V]
    {f : Î¹ â†’ Projectivization K V}

open Set Function Projectivization

namespace Projectivization

lemma linearIndependent_iff (f : Î¹ â†’ V) (hf0 : âˆ€ i, f i â‰  0) :
    LinearIndependent K f â†” Independent (fun i â†¦ mk K (f i) (hf0 i)) := by
  rw [independent_iff]
  choose c hc using fun i â†¦ exists_smul_eq_mk_rep K (f i) (hf0 i)
  convert LinearIndependent.units_smul_iff (w := fun i â†¦ (c i)â»Â¹)
  ext i
  simp [â† hc]

@[simp]
theorem independent_subsingleton [Subsingleton Î¹] (f : Î¹ â†’ Projectivization K V) :
    Independent f := by
  simp [independent_iff, rep_nonzero]

lemma independent_equiv {Î¹' : Type*} (e : Î¹ â‰ƒ Î¹') {f : Î¹' â†’ Projectivization K V} :
    Independent (f âˆ˜ e) â†” Independent f := by
  rw [independent_iff (f := f), â† linearIndependent_equiv e, independent_iff, comp_assoc]

@[simp]
lemma mk_smul_eq {u : V} (hu : u â‰  0) (c : KË£) :
    mk K (c â€¢ u) (by rwa [smul_ne_zero_iff_ne]) = Projectivization.mk K u hu :=
  (mk_eq_mk_iff ..).2 âŸ¨c, rflâŸ©

lemma mk_smul_eq' {u : V} (hu : u â‰  0) {c : K} (hc : c â‰  0) :
    mk K (c â€¢ u) (by simp [hc, hu]) = Projectivization.mk K u hu :=
  (mk_eq_mk_iff' ..).2 âŸ¨c, rflâŸ©

@[simp]
lemma independent_pair {u v : Projectivization K V} :
    Independent (fun x : ({u,v} : Set _) â†¦ x.1) := by
  rw [independent_iff]
  obtain rfl | hne := eq_or_ne u v
  Â· simp [u.rep_nonzero]
  refine (linearIndependent_restrict_pair_iff (V := V) (K := K) (fun x â†¦ x.rep) hne
    (rep_nonzero u)).2 fun c hc â†¦ hne ?_
  have hc0 : c â‰  0 := by rintro rfl; simpa [v.rep_nonzero] using hc.symm
  simpa [â† hc, mk_smul_eq' u.rep_nonzero hc0] using v.mk_rep

@[simp] lemma submodule_span_range_rep (ğ”½ W : Type*) [DivisionRing ğ”½] [AddCommGroup W]
    [Module ğ”½ W] : Submodule.span ğ”½ (range (Projectivization.rep (K := ğ”½) (V := W))) = âŠ¤ := by
  have b := Basis.ofVectorSpace ğ”½ W
  ext x
  simp only [Submodule.mem_top, iff_true]
  refine mem_of_mem_of_subset (b.mem_span x) (Submodule.span_le.2 ?_)
  rintro _ âŸ¨i, rflâŸ©
  have hi0 := b.ne_zero i
  have hmem : b i âˆˆ Submodule.span ğ”½ {(mk (K := ğ”½) (V := W) (b i) hi0).rep} := by
    rw [Submodule.mem_span_singletonâ‚€ (b.ne_zero i), â† mk_eq_mk_iff _ _ _ hi0]
    Â· simp only [mk_rep]
    exact rep_nonzero (mk ğ”½ (b i) hi0)
  exact mem_of_mem_of_subset hmem <| Submodule.span_mono <| by simp
